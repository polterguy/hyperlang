
/*
 * File responsible for databinding phrases grid.
 *
 * Optionally pass in [filter] to only show items containing specified filter,
 * and/or [offset], to list items from a specific offset.
 *
 * You can also pass in [keep-items], at which the old items in your grid will
 * be kept, and the databind operation will "append" the databound items to 
 * your grid instead.
 */





/*
 * Defaults, if no arguments have been supplied.
 */
.defaults
  offset:long:0





/*
 * Checking for arguments.
 */
micro.lambda.contract.optional:x:/..
  filter
  offset:long
  keep-items:bool





/*
 * Building our filter condition, if one is specified.
 */
_where
_where-commands
_parent-filter
_union
if:x:/../*/filter?value
  and:x:/../*/filter?value
    !=:

  /*
   * We have a filter.
   *
   * Checking if user is doing a "parent search".
   */
  split:x:/../*/filter?value
    =:":"
    trim:true
  if:x:/@split/*?count
    =:int:2

    /*
     * Column search.
     */
    if:x:/@split/0?name
      =:parent

      /*
       * Parent filter.
       */
      set:x:/@_parent-filter?value
        src:@"and p2.command like @filter"
      eval-x:x:/+/*/*
      add:x:/../*/p5.mysql.connect/*/p5.mysql.select/[0,1]
        src
          @filter:%{0}%
            :x:/@split/1?name?value

    else-if:x:/@split/0?name
      =:username

      /*
       * Username filter.
       */
      if:x:/@split/1?name
        =:null

        /*
         * Empty username.
         */
        set:x:/@_parent-filter?value
          src:@"and c.username is null"
        set:x:/@_union?value
          src:@"union all
select p.*, null as parentphrase, c.username, c.global from
  phrases p join commands c on c.id = p.commandid
  where (p.id in (select min(id) from phrases where (lang = @lang or lang = @sublang) group by commandid))
    and c.parent is null and c.username is null"

      else

        /*
         * Non empty username
         */
        set:x:/@_parent-filter?value
          src:@"and c.username = @username"
        eval-x:x:/+/*/*
        add:x:/../*/p5.mysql.connect/*/p5.mysql.select/[0,1]
          src
            @username:x:/@split/1?name?value
        set:x:/@_union?value
          src:@"union all
select p.*, null as parentphrase, c.username, c.global from
  phrases p join commands c on c.id = p.commandid
  where (p.id in (select min(id) from phrases where (lang = @lang or lang = @sublang) group by commandid))
    and c.parent is null and c.username = @username"

    else

      /*
       * Warning user that we only accept parent and username column filters.
       */
      micro.windows.info:I don't recognize that filter
        class:micro-windows-info warning
      return

  else

    /*
     * Caller supplied a filter, and it was not a column search.
     */
    set:x:/@_where?value
      src:"and command like @filter"
    set:x:/@_where-commands?value
      src:@"or (p.commandid in (select id from commands where hyperlambda like @filter)
    and p.id in (select min(id) from phrases where (lang = @lang or lang = @sublang) group by commandid))"
    eval-x:x:/+/*/*
    add:x:/../*/p5.mysql.connect/*/p5.mysql.select/[0,1]
      src
        @filter:%{0}%
          :x:/../*/filter?value
    set:x:/@_union?value
      src:@"union all
select p.*, null as parentphrase, c.username, c.global from
  phrases p join commands c on c.id = p.commandid
  where (p.id in (select min(id) from phrases where (lang = @lang or lang = @sublang) {0} group by commandid) {1})
    and c.parent is null"
        :x:/@_where?value
        :x:/@_where-commands?value

else

  /*
   * No filter, still needs union for commands not having parent commands.
   */
  set:x:/@_union?value
    src:@"union all
select p.*, null as parentphrase, c.username, c.global from
  phrases p join commands c on c.id = p.commandid
  where (p.id in (select min(id) from phrases where (lang = @lang or lang = @sublang) group by commandid))
    and c.parent is null"





/*
 * Retrieving language for user, such that we can parametrize SQL.
 */
hyperlang.language.get





/*
 * Connecting to database, and selecting records according to filter condition.
 */
p5.mysql.connect:[hyperlang]
  p5.mysql.select:@"
select p.*, p2.command as parentphrase, c.username, c.global from 
  phrases p join commands c on c.id = p.commandid,
  phrases p2 join commands c2 on c2.id = p2.commandid
  where (p.id in (select max(id) from phrases where (lang = @lang or lang = @sublang) {0} group by commandid) {1})
    and p2.id in (select min(id) from phrases where (lang = @lang or lang = @sublang) group by commandid)
    and c.parent = c2.id
    {2}
{3}
order by global desc, lastused desc, id desc limit 25 offset @offset"
    :x:/@_where?value
    :x:/@_where-commands?value
    :x:/@_parent-filter?value
    :x:/@_union?value
    @lang:x:/@hyperlang.language.get/*/lang?value
    @sublang:x:/@hyperlang.language.get/*/sublang?value
    @offset:x:(/../*/offset|/../*/.defaults/*/offset)/$?value

  /*
   * Checking if above SQL yields less than 25 results, and if so, making sure 
   * we stop the scroller.
   */
  if:x:/@p5.mysql.select/*?count
    <:int:25
    p5.web.send-javascript:"p5.hyperlang_end_of_dataset = true;"
  else
    p5.web.send-javascript:"delete p5.hyperlang_end_of_dataset;"

  /*
   * Looping through each result from above SQL, and creating one datagrid row
   * for each result.
   */
  for-each:x:/@p5.mysql.select/*
    if:x:/@_dp/#/*/global?value.int
      =:int:1
      add:x:/..for-each/**/Global
        src
          widgets
            literal
              element:span
              class:icon-check
    eval-x:x:/+/*/*/*(/Command|/Parent|/Username)|/+/*/*/*/*/.id
    add:x:/../*/micro.widgets.grid.databind
      src
        item
          .row
            .id:x:/@_dp/#/*/id?value
            onclick

              /*
               * Figuring out item's database id, and evaluating file responsible for editing item.
               */
              get-widget-property:x:/../*/_event?value
                .id
              eval-x:x:/+/*
              micro.evaluate.file:@HYPERLANG/helpers/edit-phrase.hl
                id:x:/@get-widget-property/*/*?value.long

          Command:x:/@_dp/#/*/command?value
          Parent:x:/@_dp/#/*/parentphrase?value
          Username:x:/@_dp/#/*/username?value
          Global





/*
 * Databinding datagrid, making sure we support "adding items" to it, to
 * support "never ending scrolling" to feed the grid.
 */
eval-x:x:/+/*
micro.widgets.grid.databind:hyperlang-phrases-grid
  keep-items:x:/../*/keep-items?value





/*
 * Updating "offset".
 */
+:x:(/../*/offset|/../*/.defaults/*/offset)/$?value
  _:25
p5.web.viewstate.set:hyperlang.grid-offset
  src:x:/@+?value
