
/*
 * File responsible for databinding phrases grid.
 * Optionally pass in [filter] and/or [offset].
 * You can also pass in [keep-items], at which the old items in your grid will
 * be kept, and the databind operation will "append" the databound items to 
 * your grid instead.
 *
 * Selects 25 phrases from database.
 */





/*
 * Defaults, if no arguments have been supplied.
 */
.defaults
  offset:long:0





/*
 * Checking for arguments.
 */
micro.lambda.contract.optional:x:/..
  filter
  offset:long
  keep-items:bool





/*
 * Building our filter condition, if one is specified.
 */
_where
if:x:/../*/filter?value
  and:x:/../*/filter?value
    !=:

  /*
   * Caller supplied a filter.
   */
  set:x:/@_where?value
    src:"and command like @filter"
  eval-x:x:/+/*/*
  add:x:/../*/p5.mysql.connect/*/p5.mysql.select/[0,1]
    src
      @filter:%{0}%
        :x:/../*/filter?value





/*
 * Figuring out language for current user, which is necessary to filter our results.
 */
.defaults
  voice:Karen,en-AU
p5.auth.my-settings.get
split:x:(/@p5.auth.my-settings.get/*/hyperlang/*/voice|/@.defaults/*/voice)/$?value
  =:,
split:x:/-/0/-?name
  =:-





/*
 * Connecting to database, and selecting records according to filter condition.
 */
p5.mysql.connect:[hyperlang]
  p5.mysql.select:@"
select p.id, p.command, p.commandid, p.lang, p2.command as parentphrase, c.username, c.global from 
  phrases p join commands c on c.id = p.commandid,
  phrases p2 join commands c2 on c2.id = p2.commandid
    where p.id in (select min(id) from phrases where (lang = @lang or lang = @mainlang) {0} group by commandid)
      and p2.id in (select min(id) from phrases where (lang = @lang or lang = @mainlang) group by commandid)
      and c.parent = c2.id
union all
select p3.*, null as parentphrase, c3.username, c3.global from
  phrases p3 join commands c3 on c3.id = p3.commandid
    where p3.id in (select min(id) from phrases where (lang = @lang or lang = @mainlang) {0} group by commandid)
      and c3.parent is null
order by id desc limit 25 offset @offset"
    :x:/@_where?value
    @lang:x:/@split/@split/0/-?name
    @mainlang:x:/@split/0?name
    @offset:x:(/../*/offset|/../*/.defaults/*/offset)/$?value

  /*
   * Checking if above SQL yields less than 25 results, and if so, making sure 
   * we stop the scroller.
   */
  if:x:/@p5.mysql.select/*?count
    <:int:25
    p5.web.send-javascript:"p5.hyperlang_end_of_dataset = true;"
  else
    p5.web.send-javascript:"delete p5.hyperlang_end_of_dataset;"

  /*
   * Looping through each result from above SQL, and creating one datagrid row
   * for each result.
   */
  for-each:x:/@p5.mysql.select/*
    if:x:/@_dp/#/*/global?value
      add:x:/..for-each/**/Global
        src
          widgets
            literal
              element:span
              class:icon-check
    eval-x:x:/+/*/*/*(/Command|/Language|/Parent|/Username)|/+/*/*/*/*/.id
    add:x:/../*/micro.widgets.grid.databind
      src
        item
          .row
            .id:x:/@_dp/#/*/id?value
            onclick

              /*
               * Figuring out item's database id, and evaluating file responsible for editing item.
               */
              get-widget-property:x:/../*/_event?value
                .id
              eval-x:x:/+/*
              micro.evaluate.file:@HYPERLANG/helpers/edit-phrase.hl
                id:x:/@get-widget-property/*/*?value.long

          Command:x:/@_dp/#/*/command?value
          Parent:x:/@_dp/#/*/parentphrase?value
          Language:x:/@_dp/#/*/lang?value
          Username:x:/@_dp/#/*/username?value
          Global





/*
 * Databinding datagrid.
 */
eval-x:x:/+/*
micro.widgets.grid.databind:hyperlang-phrases-grid
  keep-items:x:/../*/keep-items?value





/*
 * Updating "offset".
 */
+:x:(/../*/offset|/../*/.defaults/*/offset)/$?value
  _:25
p5.web.viewstate.set:hyperlang.grid-offset
  src:x:/@+?value
