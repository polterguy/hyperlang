
/*
 * File responsible for databinding phrases grid.
 */





/*
 * Checking for arguments.
 */
micro.lambda.contract.optional:x:/..
  filter:string





/*
 * Building our filter condition, if one is specified.
 */
_where
if:x:/../*/filter?value
  and:x:/../*/filter?value
    !=:

  /*
   * Caller supplied a filter.
   */
  set:x:/@_where?value
    src:"where command like @filter"
  eval-x:x:/+/*/*
  add:x:/../*/p5.mysql.connect/*/p5.mysql.select/[0,1]
    src
      @filter:x:/../*/filter?value





/*
 * Connecting to database, and selecting records according to filter condition.
 */
p5.mysql.connect:[hyperlang]
  p5.mysql.select:@"select * from phrases {0}"
    :x:/@_where?value

  /*
   * Looping through each result from above SQL, and creating one datagrid row
   * for each result.
   */
  for-each:x:/@p5.mysql.select/*
    eval-x:x:/+/*/*/*(/command|/lang)
    add:x:/../*/micro.widgets.grid.databind
      src
        item
          command:x:/@_dp/#/*/command?value
          lang:x:/@_dp/#/*/lang?value





/*
 * Databinding datagrid.
 */
create-widget
  element:pre
  innerValue:x:/..
micro.widgets.grid.databind:hyperlang-phrases-grid
