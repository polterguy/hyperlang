
/*
 * Edits a "phrase".
 * 
 * Expects [id] being database ID.
 */





/*
 * Verifying that mandatory arguments were supplied.
 */
micro.lambda.contract.min:x:/..
  id:long





/*
 * Checking if item is alread being edited, at which point we simply return early,
 * doing nothing.
 */
p5.web.widgets.find:hyperlang-phrases-grid
  .edit:x:/../*/id?value
if:x:/@p5.web.widgets.find/*/*?value
  return





/*
 * Connecting to database, to select record we're editing.
 */
p5.mysql.connect:[hyperlang]
  p5.mysql.select:@"select * from phrases where id = @id"
    @id:x:/../*/id?value
  p5.mysql.select:@"select * from phrases where commandid = @commandid and id != @id"
    @commandid:x:/./-/*/*/commandid?value
    @id:x:/../*/id?value
  p5.mysql.select:@"select hyperlambda, parent, global, username from commands where id = @commandid"
    @commandid:x:/./-2/*/*/commandid?value





/*
 * Preparing all synonyms for databind operation.
 */
.synonyms
for-each:x:/@p5.mysql.connect/*/p5.mysql.select/[1,2]/*
  eval-x:x:/+/*/*
  add:x:/@.synonyms
    src
      :"{0}:{1}"
        :x:/@_dp/#/*/lang?value
        :x:/@_dp/#/*/command?value
join:x:/@.synonyms/*?value
  sep:"\r\n"





/*
 * Checking if this is a "global" command.
 */
if:x:/@p5.mysql.connect/*/p5.mysql.select/[2,3]/*/*/global?value.int
  =:int:1
  add:x:/../*/create-widget/**/micro.widgets.wizard-form/*/checkbox/*/.data-field/=global/.
    src:checked





/*
 * Checking if this is a "private" command.
 */
if:x:/@p5.mysql.connect/*/p5.mysql.select/[2,3]/*/*/username?value
  add:x:/../*/create-widget/**/micro.widgets.wizard-form/*/checkbox/*/.data-field/=private/.
    src:checked





/*
 * Finding grid item, and appending a row beneath item, which allows for editing item.
 */
p5.web.widgets.find:hyperlang-phrases-grid
  .id:x:/../*/id?value
eval-x:x:/+/**/micro.widgets.wizard-form/*(/value|/*/hypereval.widgets.hyperlambda-textarea/*/value)
create-widget
  element:tr
  .edit:x:/../*/id?value
  after:x:/@p5.web.widgets.find/*/*?value
  class:no-hover
  widgets
    td
      colspan:5
      style:"background-color:#fefefe;"
      widgets
        container
          class:rounded shaded bg air-inner air-top
          widgets
            h3
              innerValue:Edit item
            micro.widgets.wizard-form
              text
                info:Phrase
                .data-field:command
                value:"{0}:{1}"
                  :x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/lang?value
                  :x:/@p5.mysql.connect/*/p5.mysql.select/[0,1]/*/*/command?value
              label
                innerValue:Hyperlambda
                style:"margin-bottom:0;"
              div
                widgets
                  hypereval.widgets.hyperlambda-textarea
                    .data-field:hyperlambda
                    value:x:/@p5.mysql.connect/*/p5.mysql.select/[2,3]/*/*/hyperlambda?value
              label
                innerValue:Synonymous words or phrases
                style:"margin-bottom:0;"
              textarea
                info:Synonymous phrases ...
                rows:10
                value:x:/@join?value
                .data-field:synonyms
              checkbox
                info:Global
                .data-field:global
                title:If checked, this phrase can be said at any time
              checkbox
                info:Private
                .data-field:private
                title:If checked, this phrase and it associated lambda is private for your user, otherwise all users can use it

            div
              class:right
              widgets
                div
                  class:strip
                  style:"display:inline-block;"
                  widgets
                    button
                      innerValue:@"<span class=""icon-check""></span>"
                      title:Saves command object, and all associated synonyms
                      style:"margin-bottom:0;"
                      events

                        /*
                         * Throws an exception if the specified language doesn't exist.
                         */
                        hyperlang._assume-language-exists

                          /*
                           * Checking to see if the specified [_arg] language exists.
                           */
                          p5.web.session.get:micro.speak.query.voices
                          if:x:/@p5.web.session.get/*/*/*/={0}
                            :x:/../*/_arg?value
                            not
                            and:x:/@p5.web.session.get/*/*/*/=~{0}-
                              :x:/../*/_arg?value
                              not
                            throw:'{0}' is not a supported language
                              :x:/../*/_arg?value

                      onclick

                        /*
                         * Serializing form, and storing values into database.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .edit
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .edit
                        micro.form.serialize:x:/@p5.web.widgets.find-first-ancestor/*/*?value

                        /*
                         * Connecting to database, and doing updates.
                         */
                        try
                          p5.mysql.connect:[hyperlang]
                            p5.mysql.transaction.begin

                              /*
                               * Figuring out commandid.
                               */
                              p5.mysql.scalar:@"select commandid from phrases where id = @id"
                                @id:x:/@get-widget-property/*/*?value

                              /*
                               * Updating "lead phrase", checking if user provided a language for it.
                               */
                              split:x:/@micro.form.serialize/*/command?value
                                =:":"
                              if:x:/@split/*?count
                                =:int:2

                                /*
                                 * User provided a language definition.
                                 */
                                hyperlang._assume-language-exists:x:/@split/0?name
                                p5.mysql.update:@"update phrases set command = @command, lang = @lang where id = @id"
                                  @command:x:/@split/0/-?name
                                  @lang:x:/@split/0?name
                                  @id:x:/@get-widget-property/*/*?value

                              else-if:x:/@split/*?count
                                =:int:1

                                /*
                                 * No language definition, assuming no change in language.
                                 */
                                p5.mysql.update:@"update phrases set command = @command where id = @id"
                                  @command:x:/@micro.form.serialize/*/command?value
                                  @id:x:/@get-widget-property/*/*?value

                              else

                                /*
                                 * User provided two or more ":" in his language phrase, which is not allowed.
                                 */
                                throw:@"Sorry, you can't use ':' in your phrases, except for describing language"

                              /*
                               * Deleting all "old" synonyms.
                               */
                              p5.mysql.delete:@"delete from phrases where commandid = @commandid and id != @id"
                                @commandid:x:/@p5.mysql.scalar?value
                                @id:x:/@get-widget-property/*/*?value

                              /*
                               * Figuring out language for current user, which is necessary to filter our results.
                               */
                              .defaults
                                voice:Karen,en-AU
                              p5.auth.my-settings.get
                              split:x:(/@p5.auth.my-settings.get/*/hyperlang/*/voice|/@.defaults/*/voice)/$?value
                                =:,
                              split:x:/@split/1?name
                                =:-

                              /*
                               * Inserting new synonyms.
                               */
                              split:x:/@micro.form.serialize/*/synonyms?value
                                =:"\r\n"
                              for-each:x:/@split/*?name

                                /*
                                 * Checking if there's an explicit language for current entry, and if not, using default.
                                 */
                                split:x:/@_dp?value
                                  =:":"
                                if:x:/@split/*?count
                                  =:int:2

                                  /*
                                   * Explicit language definition.
                                   */
                                  hyperlang._assume-language-exists:x:/@split/0?name
                                  eval-x:x:/+/*/*
                                  add:x:/..for-each/*/p5.mysql.insert
                                    src
                                      @command:x:/@split/0/-?name
                                      @lang:x:/@split/0?name

                                else-if:x:/@split/*?count
                                  =:int:1

                                  /*
                                   * No explicit language definition, relying on default.
                                   */
                                  eval-x:x:/+/*/*
                                  add:x:/..for-each/*/p5.mysql.insert
                                    src
                                      @command:x:/@_dp?value
                                      @lang:x:/..for-each/@split/@split/0?name

                                else

                                  /*
                                   * We found at least two ":" in phrase, which is not legal.
                                   */
                                  throw:@"You can only use ':' to separate between language and phrase"
                                    :x:/@_dp?value

                                p5.mysql.insert:@"insert into phrases (command, lang, commandid) values (@command, @lang, @commandid)"
                                  @commandid:x:/@p5.mysql.scalar?value

                              /*
                               * Saving actual command.
                               */
                              _username
                              if:x:/@micro.form.serialize/*/private?value

                                /*
                                 * Command is private for currently logged in user.
                                 */
                                whoami
                                set:x:/@_username?value
                                  src:x:/@whoami/*/username?value
                                

                              p5.mysql.update:@"update commands set hyperlambda = @hyperlambda, global = @global, username = @username where id = @id"
                                @hyperlambda:x:/@micro.form.serialize/*/hyperlambda?value
                                @global:x:/@micro.form.serialize/*/global?value
                                @username:x:/@_username?value
                                @id:x:/@p5.mysql.scalar?value

                              /*
                               * Committing transaction.
                               */
                              p5.mysql.transaction.commit

                              /*
                               * Providing feedback to user.
                               */
                              micro.windows.info:Command was successfully saved
                                class:micro-windows-info success

                        catch

                          /*
                           * Oops ...!!
                           */
                          micro.windows.info:x:/@message?value
                            class:micro-windows-info warning

                    button
                      innerValue:@"<span class=""icon-plus""></span>"
                      title:Creates a new child command
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Invokes event responsible for creating a new phrase to command
                         * association, passing in [pos] being currently edited command.
                         *
                         * First retrieving "phrase" id.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .edit
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .edit

                        /*
                         * Then we need to retrieve "command" id.
                         */
                        p5.mysql.connect:[hyperlang]
                          p5.mysql.scalar:@"select commandid from phrases where id = @id"
                            @id:x:/@get-widget-property/*/*?value

                        /*
                         * Then we invoke event that is responsible for creating a new phrase/command association,
                         * making sure we pass in [pos] to explicitly override the default positioning logic.
                         */
                        eval-x:x:/+/*
                        hyperlang.create-phrase2hyper-association:New child command
                          pos:x:/@p5.mysql.connect/*/p5.mysql.scalar?value

                    button
                      innerValue:@"<span class=""icon-refresh""></span>"
                      title:Clones the given command
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Invokes event responsible for creating a new phrase to command
                         * association, passing in [pos] being currently edited command.
                         *
                         * First retrieving "phrase" id.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .edit
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .edit

                        /*
                         * Then we need to retrieve "command" id.
                         */
                        p5.mysql.connect:[hyperlang]
                          p5.mysql.select:@"select parent, hyperlambda from commands where id in (select commandid from phrases where id = @id)"
                            @id:x:/@get-widget-property/*/*?value

                        /*
                         * Then we invoke event that is responsible for creating a new phrase/command association,
                         * making sure we pass in [pos] to explicitly override the default positioning logic.
                         */
                        eval-x:x:/+/*
                        hyperlang.create-phrase2hyper-association:New child command
                          pos:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/parent?value
                          hyperlambda:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/hyperlambda?value

                    button
                      innerValue:@"<span class=""icon-trash""></span>"
                      title:Deletes command object, and all associated synonyms
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Asks user to confirm deletion using a modal widget.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .edit
                        get-widget-property:x:/-/*/*?value
                          .edit
                        eval-x:x:/+/**/.id
                        create-widgets
                          micro.widgets.modal:hyperlang-confirm-deletion-modal
                            widgets
                              h3
                                innerValue:Please confirm
                              p
                                innerValue:Are you sure you want to delete this command, and all its associated synonyms? This action is permanent.
                              div
                                class:right
                                widgets
                                  div
                                    class:strip
                                    style:"display:inline-block;"
                                    widgets
                                      button
                                        innerValue:Yes
                                        style:"margin-bottom:0;"
                                        oninit

                                          /*
                                           * Setting initial focus to "Yes" button.
                                           */
                                          micro.page.set-focus:x:/../*/_event?value

                                        onclick

                                          /*
                                           * Deleting command, and deleting row from grid.
                                           */
                                          .id:x:/../*/get-widget-property/*/*?value
                                          p5.mysql.connect:[hyperlang]
                                            p5.mysql.delete:@"delete from commands where id in (select commandid from phrases where id = @id)"
                                              @id:x:/@.id?value
                                          p5.web.widgets.find
                                            .id:x:/@.id?value
                                          delete-widget:x:/-/*/*?value
                                          p5.web.widgets.find
                                            .edit:x:/@.id?value
                                          delete-widget:x:/-/*/*?value
                                          micro.windows.info:Command object was successfully deleted
                                            class:micro-windows-info success

                                          /*
                                           * Deletes modal widget.
                                           */
                                          delete-widget:hyperlang-confirm-deletion-modal

                                      button
                                        innerValue:No
                                        style:"margin-bottom:0;"
                                        onclick

                                          /*
                                           * Simply deletes modal widget.
                                           */
                                          delete-widget:hyperlang-confirm-deletion-modal

                    button
                      innerValue:@"<span class=""icon-close""></span>"
                      title:Close editor for command object without saving
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Simply deleting editor widget and row entirely.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .edit
                        delete-widget:x:/-/*/*?value
