
/*
 * Edits a "phrase".
 * 
 * Expects [id] being database ID.
 */





/*
 * Verifying that mandatory arguments were supplied.
 */
micro.lambda.contract.min:x:/..
  id:long





/*
 * Checking if item is alread being edited, at which point we simply return early,
 * doing nothing.
 */
p5.web.widgets.find:hyperlang-phrases-grid
  .edit:x:/../*/id?value
if:x:/@p5.web.widgets.find/*/*?value
  return





/*
 * Connecting to database, to select record we're editing.
 */
p5.mysql.connect:[hyperlang]
  p5.mysql.select:@"select * from phrases where id = @id"
    @id:x:/../*/id?value
  p5.mysql.select:@"select * from phrases where commandid = @commandid"
    @commandid:x:/./-/*/*/commandid?value
  p5.mysql.select:@"select hyperlambda, parent, global, username from commands where id = @commandid"
    @commandid:x:/./-2/*/*/commandid?value





/*
 * Preparing all synonyms for databind operation.
 */
hyperlang.language.get
.synonyms
for-each:x:/@p5.mysql.connect/*/p5.mysql.select/[1,2]/*

  /*
   * Checking is currently iterated synonym's language declaration equals the
   * main language for user, at which point we do not prepend the language declaration
   * for it.
   */
  if:x:/@hyperlang.language.get/*/lang?value
    =:x:/@_dp/#/*/lang?value

    /*
     * Language for currently iterated synonym is the same as the root language for
     * user, hence we don't explicitly prepend the phrase with its language definition.
     */
    eval-x:x:/+/*/*
    add:x:/@.synonyms
      src
        :x:/@_dp/#/*/command?value

  else

    /*
     * Language declaration for currently iterated phrase is different from user's
     * main language of choice.
     */
    eval-x:x:/+/*/*
    add:x:/@.synonyms
      src
        :"{0}:{1}"
          :x:/@_dp/#/*/lang?value
          :x:/@_dp/#/*/command?value

join:x:/@.synonyms/*?value
  sep:"\r\n"





/*
 * Checking if this is a "global" command.
 */
if:x:/@p5.mysql.connect/*/p5.mysql.select/[2,3]/*/*/global?value.int
  =:int:1
  add:x:/../*/create-widget/**/micro.widgets.wizard-form/*/checkbox/*/.data-field/=global/.
    src:checked





/*
 * Checking if this is a "private" command.
 */
if:x:/@p5.mysql.connect/*/p5.mysql.select/[2,3]/*/*/username?value
  add:x:/../*/create-widget/**/micro.widgets.wizard-form/*/checkbox/*/.data-field/=private/.
    src:checked





/*
 * Finding grid item, and appending a row beneath item, which allows for editing item.
 */
p5.web.widgets.find:hyperlang-phrases-grid
  .id:x:/../*/id?value
eval-x:x:/+/**/micro.widgets.wizard-form/*(/value|/*/hypereval.widgets.hyperlambda-textarea/*/value)
create-widget
  element:tr
  .edit:x:/../*/id?value
  after:x:/@p5.web.widgets.find/*/*?value
  class:no-hover
  widgets
    td
      colspan:5
      style:"background-color:#fefefe;"
      widgets
        container
          class:rounded shaded bg air-inner air-top
          widgets
            micro.widgets.wizard-form
              label
                innerValue:Phrase(s)
                style:"margin-bottom:0;"
              textarea
                info:Phrase(s), one for each line
                rows:5
                value:x:/@join?value
                .data-field:synonyms
              label
                innerValue:Hyperlambda
                style:"margin-bottom:0;"
              div
                widgets
                  hypereval.widgets.hyperlambda-textarea
                    .data-field:hyperlambda
                    value:x:/@p5.mysql.connect/*/p5.mysql.select/[2,3]/*/*/hyperlambda?value
              checkbox
                info:Global
                .data-field:global
                title:If checked, this phrase can be said at any time
              checkbox
                info:Private
                .data-field:private
                title:If checked, this phrase and it associated lambda is private for your user, otherwise all users can use it

            div
              class:right
              widgets
                div
                  class:strip
                  style:"display:inline-block;"
                  widgets
                    button
                      innerValue:@"<span class=""icon-check""></span>"
                      title:Saves command object, and all associated synonyms
                      style:"margin-bottom:0;"
                      events

                        /*
                         * Throws an exception if the specified language doesn't exist.
                         */
                        hyperlang._assume-language-exists

                          /*
                           * Checking to see if the specified [_arg] language exists.
                           */
                          p5.web.session.get:micro.speak.query.voices
                          if:x:/@p5.web.session.get/*/*/*/={0}
                            :x:/../*/_arg?value
                            not
                            and:x:/@p5.web.session.get/*/*/*/=~{0}-
                              :x:/../*/_arg?value
                              not
                            throw:'{0}' is not a supported language
                              :x:/../*/_arg?value

                      onclick

                        /*
                         * Serializing form, and storing values into database.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .edit
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .edit
                        micro.form.serialize:x:/@p5.web.widgets.find-first-ancestor/*/*?value

                        /*
                         * Connecting to database, and doing updates.
                         */
                        try
                          p5.mysql.connect:[hyperlang]
                            p5.mysql.transaction.begin

                              /*
                               * Figuring out commandid.
                               */
                              p5.mysql.scalar:@"select commandid from phrases where id = @id"
                                @id:x:/@get-widget-property/*/*?value

                              /*
                               * Deleting all "old" phrases.
                               */
                              p5.mysql.delete:@"delete from phrases where commandid = @commandid"
                                @commandid:x:/@p5.mysql.scalar?value

                              /*
                               * Retrieving language for current user, which is necessary to filter our results.
                               */
                              hyperlang.language.get

                              /*
                               * Inserting new phrases.
                               */
                              split:x:/@micro.form.serialize/*/synonyms?value
                                =:"\r\n"
                              _new-phrase-id
                              p5.types.date.now
                              for-each:x:/@split/*?name

                                /*
                                 * Checking if there's an explicit language for current entry, and if not, using default.
                                 */
                                split:x:/@_dp?value
                                  =:":"
                                if:x:/@split/*?count
                                  =:int:2

                                  /*
                                   * Explicit language definition.
                                   */
                                  hyperlang._assume-language-exists:x:/@split/0?name
                                  eval-x:x:/+/*/*
                                  add:x:/..for-each/*/p5.mysql.insert
                                    src
                                      @command:x:/@split/0/-?name
                                      @lang:x:/@split/0?name

                                else-if:x:/@split/*?count
                                  =:int:1

                                  /*
                                   * No explicit language definition, relying on default.
                                   */
                                  eval-x:x:/+/*/*
                                  add:x:/..for-each/*/p5.mysql.insert
                                    src
                                      @command:x:/@_dp?value
                                      @lang:x:/@hyperlang.language.get/*/lang?value

                                else

                                  /*
                                   * We found at least two ":" in phrase, which is not legal.
                                   */
                                  throw:@"You can only use ':' to separate between language and phrase"
                                    :x:/@_dp?value

                                /*
                                 * Inserting into phrases, making sure we store database id for the
                                 * first insertion, such that we can update our row's database reference.
                                 */
                                p5.mysql.insert:@"insert into phrases (command, lang, commandid, lastused) values (@command, @lang, @commandid, @lastused)"
                                  @commandid:x:/@p5.mysql.scalar?value
                                  @lastused:x:/@p5.types.date.now?value
                                if:x:/@_new-phrase-id?value
                                  not
                                  set:x:/@_new-phrase-id?value
                                    src:x:/@p5.mysql.insert/*/id?value

                              /*
                               * Saving actual command.
                               */
                              _username
                              if:x:/@micro.form.serialize/*/private?value

                                /*
                                 * Command is private for currently logged in user.
                                 */
                                whoami
                                set:x:/@_username?value
                                  src:x:/@whoami/*/username?value
                                

                              p5.mysql.update:@"update commands set hyperlambda = @hyperlambda, global = @global, username = @username where id = @id"
                                @hyperlambda:x:/@micro.form.serialize/*/hyperlambda?value
                                @global:x:/@micro.form.serialize/*/global?value
                                @username:x:/@_username?value
                                @id:x:/@p5.mysql.scalar?value

                              /*
                               * Committing transaction.
                               */
                              p5.mysql.transaction.commit

                              /*
                               * Closing editor widget.
                               */
                              delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value

                              /*
                               * Making sure update database id for currently updated row.
                               */
                              p5.web.widgets.find
                                .id:x:/../*/get-widget-property/*/*?value
                              set-widget-property:x:/@p5.web.widgets.find/*/*?value
                                .id:x:/@_new-phrase-id?value

                        catch

                          /*
                           * Oops ...!!
                           */
                          micro.windows.info:x:/@message?value
                            class:micro-windows-info warning

                    button
                      innerValue:@"<span class=""icon-plus""></span>"
                      title:Creates a new child command
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Invokes event responsible for creating a new phrase to command
                         * association, passing in [pos] being currently edited command.
                         *
                         * First retrieving "phrase" id.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .edit
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .edit

                        /*
                         * Then we need to retrieve "command" id.
                         */
                        p5.mysql.connect:[hyperlang]
                          p5.mysql.scalar:@"select commandid from phrases where id = @id"
                            @id:x:/@get-widget-property/*/*?value

                        /*
                         * Then we invoke event that is responsible for creating a new phrase/command association,
                         * making sure we pass in [pos] to explicitly override the default positioning logic.
                         */
                        eval-x:x:/+/*
                        hyperlang.create-phrase2hyper-association:New child command
                          pos:x:/@p5.mysql.connect/*/p5.mysql.scalar?value
                          force-parent:bool:true

                    button
                      innerValue:@"<span class=""icon-refresh""></span>"
                      title:Clones the given command
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Invokes event responsible for creating a new phrase to command
                         * association, passing in [pos] being currently edited command.
                         *
                         * First retrieving "phrase" id.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .edit
                        get-widget-property:x:/@p5.web.widgets.find-first-ancestor/*/*?value
                          .edit

                        /*
                         * Then we need to retrieve "command" id.
                         */
                        p5.mysql.connect:[hyperlang]
                          p5.mysql.select:@"select parent, hyperlambda from commands where id in (select commandid from phrases where id = @id)"
                            @id:x:/@get-widget-property/*/*?value

                        /*
                         * Then we invoke event that is responsible for creating a new phrase/command association,
                         * making sure we pass in [pos] to explicitly override the default positioning logic, but only
                         * if current command actually has a parent.
                         */
                        if:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/parent?value
                          add:x:/../*/hyperlang.create-phrase2hyper-association
                            src
                              pos:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/parent?value
                        eval-x:x:/+/*
                        hyperlang.create-phrase2hyper-association:New cloned command
                          hyperlambda:x:/@p5.mysql.connect/*/p5.mysql.select/*/*/hyperlambda?value
                          force-parent:bool:true

                    button
                      innerValue:@"<span class=""icon-trash""></span>"
                      title:Deletes command object, and all associated synonyms
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Asks user to confirm deletion using a modal widget.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .edit
                        get-widget-property:x:/-/*/*?value
                          .edit
                        eval-x:x:/+/**/.id
                        create-widgets
                          micro.widgets.modal:hyperlang-confirm-deletion-modal
                            widgets
                              h3
                                innerValue:Please confirm
                              p
                                innerValue:Are you sure you want to delete this command, and all its associated synonyms? This action is permanent.
                              div
                                class:right
                                widgets
                                  div
                                    class:strip
                                    style:"display:inline-block;"
                                    widgets
                                      button
                                        innerValue:Yes
                                        style:"margin-bottom:0;"
                                        oninit

                                          /*
                                           * Setting initial focus to "Yes" button.
                                           */
                                          micro.page.set-focus:x:/../*/_event?value

                                        onclick

                                          /*
                                           * Deleting command, and deleting row from grid.
                                           */
                                          .id:x:/../*/get-widget-property/*/*?value
                                          p5.mysql.connect:[hyperlang]
                                            p5.mysql.delete:@"delete from commands where id in (select commandid from phrases where id = @id)"
                                              @id:x:/@.id?value
                                          p5.web.widgets.find
                                            .id:x:/@.id?value
                                          delete-widget:x:/-/*/*?value
                                          p5.web.widgets.find
                                            .edit:x:/@.id?value
                                          delete-widget:x:/-/*/*?value

                                          /*
                                           * Deletes modal widget.
                                           */
                                          delete-widget:hyperlang-confirm-deletion-modal

                                      button
                                        innerValue:No
                                        style:"margin-bottom:0;"
                                        onclick

                                          /*
                                           * Simply deletes modal widget.
                                           */
                                          delete-widget:hyperlang-confirm-deletion-modal

                    button
                      innerValue:@"<span class=""icon-close""></span>"
                      title:Close editor for command object without saving
                      style:"margin-bottom:0;"
                      onclick

                        /*
                         * Simply deleting editor widget and row entirely.
                         */
                        p5.web.widgets.find-first-ancestor:x:/../*/_event?value
                          .edit
                        delete-widget:x:/@p5.web.widgets.find-first-ancestor/*/*?value
