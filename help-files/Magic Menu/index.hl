/*
 * This is our main "landing page" for our Magic Menu help files.
 */
h2
  innerValue:Magic Menu

img
  style:"margin-left:1rem; float:right;"
  src:"https://phosphorusfive.files.wordpress.com/2018/03/magic-menu-screenshot-2.png"

p
  innerValue:@"The Magic Menu is a common navigation system, that you can use in your own modules,
that allows for easily navigating your apps. It also supports speech recognition, and speech synthesis, allowing
you to exclusively use your voice to command your Phosphorus Five modules. In addition, it also contains a rich
API, allowing for you to interact with it, and override almost its entire behaviour as you see fit."

p
  innerValue:@"The Magic Menu literally supports creating thousands of menu items, which can be selected by
speaking to your computer, and easily allows the user to search and page through these menu items. This makes
it an extremely scalable menu system, and you could in theory use it to create entire apps if you wanted to."

/*
 * All files at "root" of sub section.
 */
container
  element:ul
  oninit

    /*
     * Retrieving all other Hyperlambda files in the help folder.
     */
    list-files:@MAGIC/help-files/Magic Menu/
      filter:.hl|.md

    /*
     * Removing "index.hl".
     */
    set:x:@"/@list-files/*/"":regex:/index.hl$/"""

    /*
     * Looping through each Hyperlambda file (except of course "index.hl").
     */
    for-each:x:/@list-files/*

      /*
       * Figuring out filename, without extension, and removing numbering parts.
       */
      split:x:/@_dp/#?name
        =:/
        =:.
      replace:x:/@split/0/-2?name
        src:regex:/[0-9]+ - /

      /*
       * Creating a list item, containing a hyperlink button, which once clicked,
       * loads and displays the specified help file.
       */
      create-widget
        parent:x:/../*/_event?value
        element:li
        widgets
          a
            href:#
            innerValue:x:/@replace?value
            .file:x:/@_dp/#?name
            onclick

              /*
               * Retrieving file name, loading and displaying file, by invoking common
               * widget lambda event, which does the heavy lifting for us.
               */
              get-widget-property:x:/../*/_event?value
                .file
              desktop.help.display-file:x:/@get-widget-property/*/*?value
