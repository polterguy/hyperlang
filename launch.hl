
/*
 * File responsible for starting module.
 */





/*
 * Including Micro and its "serious" skin.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:@MICRO/media/fonts.css





/*
 * Creating our main wire frame.
 */
create-widget:hyperlang-main-container
  class:container hyperlang-editor
  widgets
    div
      class:row
      widgets
        div
          class:col-70 right
          widgets
            div
              class:strip fill
              widgets
                input:hyperlang-search-textbox
                  type:text
                  placeholder:Filter ...
                  onkeydown:@"if (event.keyCode == 13) {p5.$('hyperlang-search-button').raise('onclick');return false;}"

                container:hyperlang-select-language
                  element:select
                  style:"width:40%;"
                  oninit

                    /*
                     * Selecting all supported languages, which depends upon
                     * your browser's supported speech languages.
                     */
                    micro.speak.query-voices
                      onfinish

                        /*
                         * Looping through each voice, and creating one option element for each.
                         *
                         * But first creating our "select voice" option element.
                         */
                        create-widget
                          parent:hyperlang-select-language
                          element:option
                          innerValue:Select voice ...
                          value:_default
                        for-each:x:/../*/voices/*
                          create-widget
                            parent:hyperlang-select-language
                            element:option
                            innerValue:{0},{1}
                              :x:/@_dp/#?name
                              :x:/@_dp/#?value
                            value:{0},{1}
                              :x:/@_dp/#?name
                              :x:/@_dp/#?value

                        /*
                         * Setting widget's initial value to language setting for user, if possible.
                         */
                        p5.auth.my-settings.get
                        if:x:/@p5.auth.my-settings.get/*/hyperlang/*/voice?value
                          set-widget-property:hyperlang-select-language
                            value:x:/@p5.auth.my-settings.get/*/hyperlang/*/voice?value

                  onchange

                    /*
                     * Updating user's settings according to voice selected.
                     */
                    get-widget-property:x:/../*/_event?value
                      value
                    p5.auth.my-settings.get
                    if:x:/@p5.auth.my-settings.get/*/hyperlang
                      not
                      add:x:/@p5.auth.my-settings.get
                        src:hyperlang
                    set:x:/@p5.auth.my-settings.get/*/hyperlang/*/voice
                    eval-x:x:/+/*/*
                    add:x:/@p5.auth.my-settings.get/*/hyperlang
                      src
                        voice:x:/@get-widget-property/*/*?value
                    add:x:/../*/p5.auth.my-settings.set
                      src:x:/@p5.auth.my-settings.get/*
                    p5.auth.my-settings.set

                    /*
                     * Re-databinding grid.
                     */
                    micro.evaluate.file:@HYPERLANG/helpers/databind-grid.hl

                button:hyperlang-search-button
                  innerValue:@"<span class=""icon-search""></span>"
                  title:Search
                  onclick

                    /*
                     * Retrieving filter, and re-databinding grid, now with the specified filter condition.
                     */
                    get-widget-property:hyperlang-search-textbox
                      value
                    eval-x:x:/+/*
                    micro.evaluate.file:@HYPERLANG/helpers/databind-grid.hl
                      filter:x:/@get-widget-property/*/*?value

                    /*
                     * For simplicity, we set focus to search textbox again.
                     */
                    micro.page.set-focus:hyperlang-search-textbox

                    /*
                     * Making sure we store filter.
                     */
                    p5.web.viewstate.set:hyperlang.grid-filter
                      src:x:/@get-widget-property/*/*?value

        div
          class:col right
          widgets
            div
              class:strip toolbar
              style:"display:inline-block;"
              widgets

                button:hyperlang-add-command
                  innerValue:@"<span class=""icon-plus""></span>"
                  title:Add new command
                  style:"padding-left:45px;padding-right:45px;"
                  onclick

                button
                  innerValue:@"<span class=""icon-home""></span>"
                  title:Close Hyperlang
                  onclick

                    /*
                     * Redirecting user to server's root URL.
                     */
                    p5.web.get-root-location
                    p5.web.set-location:x:/-?value

    div
      class:row
      widgets
        container:hyperlang-content
          class:col-100
          widgets

            /*
             * Grid containing all words and phrasese Hyperlang understands.
             */
            micro.widgets.grid:hyperlang-phrases-grid
              class:hover striped
              columns
                Command
                Parent
                Language
                Username
                Global
              oninit

                /*
                 * Evaluating file that databinds grid.
                 */
                micro.evaluate.file:@HYPERLANG/helpers/databind-grid.hl

                /*
                 * Including JavaScript to determine if user is at the bottom of page,
                 * at which point we "auto-feed" grid with more items.
                 */
                p5.web.include-javascript:@"
window.onscroll = function() {
  if ((window.innerHeight + window.scrollY) >= document.body.offsetHeight) {
    if (!p5.hyperlang_end_of_dataset) {
      if(p5.hyperlang_ongoing_scroll === true) {
        return;
      }
      p5.hyperlang_ongoing_scroll = true;
      var el = p5.$('hyperlang-phrases-grid');
      el.raise('.onfeed',{
        onsuccess:function() {
          p5.hyperlang_ongoing_scroll = false;
        }
      });
    }
  }
};"
              .onfeed

                /*
                 * Invoked when grid needs more items.
                 */
                p5.web.viewstate.get:hyperlang.grid-offset
                p5.web.viewstate.get:hyperlang.grid-filter
                eval-x:x:/+/*(/offset|/filter)
                micro.evaluate.file:@HYPERLANG/helpers/databind-grid.hl
                  keep-items:true
                  offset:x:/../*/p5.web.viewstate.get/[0,1]/*?value
                  filter:x:/../*/p5.web.viewstate.get/[1,2]/*?value
