
/*
 * File responsible for starting module.
 */





/*
 * Checking if this is an upload request.
 */
split:x:/../*/url?value
  =:/
if:x:/@split/0/-?name
  =:upload

  /*
   * Invoking event responsible for handling uploads, and returning early.
   */
  micro.evaluate.file:@HYPERLANG/helpers/handle-upload-request.hl
  return





/*
 * Including Micro and its "serious" skin.
 */
p5.web.include-css-file:@MICRO/media/main.css
p5.web.include-css-file:@MICRO/media/skins/serious.css
p5.web.include-css-file:@MICRO/media/fonts.css





/*
 * Verifying user is logged in as root, and if not, 
 * forcing the user to login before we proceed.
 */
whoami
if:x:/@whoami/*/role?value
  !=:root

  /*
   * User is not logged in as root.
   */
  p5.core.login
    message:You'll need to login with a root account to access this module

  /*
   * Returning early to abort evaluating the rest of our file.
   */
  return





/*
 * Creating our main wire frame.
 */
create-widget:hyperlang-main-container
  class:container hyperlang-editor
  widgets
    div
      class:row
      widgets
        div
          class:col-67 right
          widgets
            div
              class:strip fill
              widgets
                input:hyperlang-search-textbox
                  type:text
                  placeholder:Filter ...
                  onkeydown:@"if (event.keyCode == 13) {p5.$('hyperlang-search-button').raise('onclick');return false;}"

                button:hyperlang-clear-search-button
                  innerValue:@"<span class=""icon-remove""></span>"
                  title:Clears filter
                  disabled
                  onclick

                    /*
                     * Clears filter and invokes Ajax event for search button.
                     */
                    set-widget-property:hyperlang-search-textbox
                      value:
                    p5.web.widgets.ajax-events.raise:hyperlang-search-button
                      onclick

                container:hyperlang-select-language
                  element:select
                  style:"width:40%;"
                  oninit

                    /*
                     * Selecting all supported languages, which depends upon
                     * your browser's supported speech languages.
                     */
                    micro.speak.query-voices
                      onfinish

                        /*
                         * Looping through each voice, and creating one option element for each.
                         *
                         * But first creating our "select voice" option element.
                         */
                        create-widget
                          parent:hyperlang-select-language
                          element:option
                          innerValue:Select voice ...
                          value:_default
                        for-each:x:/../*/voices/*
                          create-widget
                            parent:hyperlang-select-language
                            element:option
                            innerValue:{0},{1}
                              :x:/@_dp/#?name
                              :x:/@_dp/#?value
                            value:{0},{1}
                              :x:/@_dp/#?name
                              :x:/@_dp/#?value

                        /*
                         * Setting widget's initial value to language setting for user, if possible.
                         */
                        hyperlang.settings.get-language
                        set-widget-property:hyperlang-select-language
                          value:{0},{1}
                            :x:/@hyperlang.settings.get-language/*/voice?value
                            :x:/@hyperlang.settings.get-language/*/sublang?value

                  onchange

                    /*
                     * Updating user's settings according to voice selected.
                     */
                    get-widget-property:x:/../*/_event?value
                      value
                    hyperlang.settings.set-language:x:/@get-widget-property/*/*?value

                    /*
                     * Re-databinding grid.
                     */
                    micro.evaluate.file:@HYPERLANG/helpers/databind-grid.hl

                button:hyperlang-search-button
                  innerValue:@"<span class=""icon-search""></span>"
                  title:Search
                  onclick

                    /*
                     * Retrieving filter, and re-databinding grid, now with the specified filter condition.
                     */
                    get-widget-property:hyperlang-search-textbox
                      value
                    eval-x:x:/+/*
                    micro.evaluate.file:@HYPERLANG/helpers/databind-grid.hl
                      filter:x:/@get-widget-property/*/*?value

                    /*
                     * For simplicity, we set focus to search textbox again.
                     */
                    micro.page.set-focus:hyperlang-search-textbox

                    /*
                     * Making sure we store filter.
                     */
                    p5.web.viewstate.set:hyperlang.grid-filter
                      src:x:/@get-widget-property/*/*?value

                    /*
                     * Enabling clear button, if we have a filter, otherwise disabling it.
                     */
                    if:x:/@get-widget-property/*/*?value
                      !=:
                      delete-widget-property:hyperlang-clear-search-button
                        disabled
                    else
                      set-widget-property:hyperlang-clear-search-button
                        disabled

        div
          class:col right
          widgets
            div
              class:strip toolbar
              style:"display:inline-block;"
              widgets

                button:hyperlang-add-command
                  innerValue:@"<span class=""icon-plus""></span>"
                  title:Add new command
                  style:"padding-left:45px;padding-right:45px;"
                  onclick

                    /*
                     * Creates a new command, by invoking file responsible for doing just that..
                     */
                    hyperlang.command.create

                button
                  innerValue:@"<span class=""icon-download""></span>"
                  title:Download dictionary
                  onclick

                    /*
                     * Downloads a backup of entire database as a zip file.
                     */
                    micro.evaluate.file:@HYPERLANG/helpers/export-dictionary.hl
                    hyperlang.backup.download

                button
                  innerValue:@"<span class=""icon-upload""></span>"
                  title:Upload dictionary file
                  onclick:@"p5.dropzone.browse();event.stopPropagation(true);return false;"
                button
                  innerValue:@"<span class=""icon-home""></span>"
                  title:Close Hyperlang
                  onclick

                    /*
                     * Redirecting user to server's root URL.
                     */
                    p5.web.get-root-location
                    p5.web.set-location:x:/-?value

    div
      class:row
      widgets
        container:hyperlang-content
          class:col-100
          widgets

            /*
             * Grid containing all words and phrasese Hyperlang understands.
             */
            micro.widgets.grid:hyperlang-phrases-grid
              class:hover striped hyperlang-grid
              columns
                Command
                Parent
                Username
                Global
              oninit

                /*
                 * Evaluating file that databinds grid.
                 */
                micro.evaluate.file:@HYPERLANG/helpers/databind-grid.hl

                /*
                 * Including JavaScript to determine if user is at the bottom of page,
                 * at which point we "auto-feed" grid with more items.
                 */
                p5.web.include-javascript:@"
window.onscroll = function() {
  if ((window.innerHeight + window.scrollY) >= document.body.offsetHeight) {
    if (!p5.hyperlang_end_of_dataset) {
      if(p5.hyperlang_ongoing_scroll === true) {
        return;
      }
      p5.hyperlang_ongoing_scroll = true;
      var el = p5.$('hyperlang-phrases-grid');
      el.raise('.onfeed',{
        onsuccess:function() {
          p5.hyperlang_ongoing_scroll = false;
        }
      });
    }
  }
};"
              .onfeed

                /*
                 * Invoked when grid needs more items.
                 */
                p5.web.viewstate.get:hyperlang.grid-offset
                p5.web.viewstate.get:hyperlang.grid-filter
                eval-x:x:/+/*(/offset|/filter)
                micro.evaluate.file:@HYPERLANG/helpers/databind-grid.hl
                  keep-items:true
                  offset:x:/../*/p5.web.viewstate.get/[0,1]/*?value
                  filter:x:/../*/p5.web.viewstate.get/[1,2]/*?value


/*
 * Creating dropzone for entire page, to allow for drag and drop uploading of language files.
 */
micro.page.create-dropzone
  filter:zip|hl
  url:/hyperlang/upload
  .onfinish:@"function(uid, no_files, no_errors) {window.location.replace(window.location.href);}"
