
/*
 * Creates the event that pops one position off from the hierarchy.
 *
 * Optionally pass in [no-post] to avoid broadcasting change of position,
 * or [_arg] being an integer value, to pop [_arg] positions in one invocation.
 */
create-event:hyperlang.grammar-position.pop

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.optional:x:/..
    _arg:int

  /*
   * Removes the last position from our chain, if any, and returns the new 
   * position to caller.
   */
  p5.web.viewstate.get:hyperlang.position

  /*
   * Checking if caller supplied an integer [_arg] value.
   */
  if:x:/../*/_arg?value

    /*
     * Making sure we pop "n" positions.
     */
    while:x:/../*/_arg?value.int
      !=:int:0
      set:x:/@p5.web.viewstate.get/*/*/0/-
      set:x:/../*/_arg?value.int
        -:x:/../*/_arg?value.int
          _:1

  else

    /*
     * No integer value, popping a single entry.
     */
    set:x:/@p5.web.viewstate.get/*/*/0/-

  /*
   * Updating grammar tree, now with positions "popped".
   */
  add:x:/../*/p5.web.viewstate.set/*
    src:x:/@p5.web.viewstate.get/*/*
  p5.web.viewstate.set:hyperlang.position
    src

  /*
   * Posting "changed" event, but only if caller did not explicitly stop 
   * us from doing such.
   */
  if:x:/../*/no-post?value
    !=:bool:true
    hyperlang.position.changed
