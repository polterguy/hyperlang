
/*
 * Creates the event that will return the possible options that user has from his
 * current position.
 *
 * Optionally pass in [limit], [offset] and [filter] to further filter your results.
 */
create-event:hyperlang.grammar-position.list-options

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.optional:x:/..
    limit:long
    offset:long
    filter:string

  /*
   * Defaults, in case no parameters are passed in.
   */
  .defaults
    limit:long:10
    offset:long:0

  /*
   * Retrieves the current position, username, and current language to parametrise SQL.
   */
  whoami
  hyperlang.grammar-position.get
  hyperlang.settings.get-language

  /*
   * Dynamically building our SQL, according to filter conditionins.
   */
  _sql:@"select p.id, p.command from 
    phrases p join commands c on c.id = p.commandid
    where (p.id in (select min(id) from phrases where (lang = @lang or lang = @sublang) {{filter}} group by commandid))"

  /*
   * Checking if a filter condition was supplied.
   */
  if:x:/../*/filter?value

    /*
     * Caller supplied a [filter].
     *
     * Checking that filter doesn't try to do an SQL injection.
     */
    if
      fetch:x:/0/0?name
        match:x:/../*/filter?value
          src:regex:/\{\{filter/
      throw:Illegal filter condition

    /*
     * Filtering only on command.
     */
    set:x:/@_sql?value
      replace:x:/@_sql?value
        src:{{filter}}
        dest:"and p.command like @filter"

  else

    /*
     * No [filter] was supplied.
     */
    set:x:/@_sql?value
      replace:x:/@_sql?value
        src:{{filter}}

  /*
   * Checking parent, and parametrizing SQL accordingly.
   */
  _parent
  if:x:/@hyperlang.grammar-position.get?value

    /*
     * We have an existing parent.
     */
    set:x:/@_parent?value
      src:"and c.parent = @parent"
    add:x:/../*/p5.mysql.connect/*/p5.mysql.select
      src
        @parent:x:/@hyperlang.grammar-position.get?value

  else

    /*
     * No current parent.
     */
    set:x:/@_parent?value
      src:"and c.parent is null"

  /*
   * Adding "tail" to SQL.
   *
   * Notice, we show "last used" commands first, then the commands that 
   * were created last.
   */
  set:x:/@_sql?value
    src:"{0} {1} order by lastused desc, id desc limit @limit offset @offset"
      :x:/@_sql?value
      :x:/@_parent?value

  /*
   * Connecting to database, and retrieving all possible options from current 
   * position in tree.
   */
  p5.mysql.connect:[hyperlang]
    p5.mysql.select:x:/@_sql?value
      @username:x:/@whoami/*/username?value
      @lang:x:/@hyperlang.settings.get-language/*/lang?value
      @sublang:x:/@hyperlang.settings.get-language/*/sublang?value
      @limit:x:(/../*/limit|/@.defaults/*/limit)/$?value.long
      @offset:x:(/../*/offset|/@.defaults/*/offset)/$?value.long

    /*
     * Parametrizing return invocation below.
     */
    for-each:x:/@p5.mysql.select/*
      add:x:/../*/return
        src:@"{0}:{1}"
          :x:/@_dp/#/*/id?value
          :x:/@_dp/#/*/command?value

  /*
   * Returning results to caller.
   */
  return
