
/*
 * Creates the event that sets the current position in our command hierarchy.
 *
 * Requires [_arg] being the database ID of a "command" from magic-menu.
 */
create-event:magic-menu.grammar-position.push

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:long

  /*
   * Retrieving current position, to see if specified position is the
   * same as our current position, and if it is, we don't evaluate the rest
   * of our lambda, but return early instead.
   */
  magic-menu.grammar-position.get
  if:x:/@magic-menu.grammar-position.get?value
    and:x:/@magic-menu.grammar-position.get?value.long
      =:x:/../*/_arg?value.long

    /*
     * No reasons to push the same ID twice.
     */
    return

  /*
   * Sets the current position to the specified [_arg].
   */
  p5.web.viewstate.get:magic-menu.position
  add:x:/../*/p5.web.viewstate.set/*/*
    src:x:/@p5.web.viewstate.get/*/*/*
  add:x:/../*/p5.web.viewstate.set/*/*
    src:":{0}"
      :x:/../*/_arg?value
  p5.web.viewstate.set:magic-menu.position
    src
      positions

  /*
   * Posting "changed" event.
   */
  magic-menu.position.changed
