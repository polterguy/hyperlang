
/*
 * Pops off one position from our "grammar-tree" hierarchy.
 *
 * Optionally pass in [_arg] being an integer value, to pop off [_arg] number of
 * positions in one invocation.
 */
create-event:magic-menu.grammar-position.pop

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.optional:x:/..
    _arg:int

  /*
   * Retrieving current position, to see if there are any positions,
   * and if not, we don't evaluate the rest of this lambda.
   */
  magic-menu.grammar-position.get
  if:x:/@magic-menu.grammar-position.get?value
    not

    /*
     * Nothing to pop.
     */
    return

  /*
   * Removes the last position from our chain, if any, and returns the new 
   * position to caller.
   */
  p5.web.viewstate.get:magic-menu.position

  /*
   * Checking if caller supplied an integer [_arg] value.
   */
  if:x:/../*/_arg?value

    /*
     * Making sure we pop "n" positions.
     */
    while:x:/../*/_arg?value.int
      !=:int:0
      set:x:/@p5.web.viewstate.get/*/*/0/-
      set:x:/../*/_arg?value.int
        -:x:/../*/_arg?value.int
          _:1

  else

    /*
     * No integer value, popping a single entry.
     */
    set:x:/@p5.web.viewstate.get/*/*/0/-

  /*
   * Updating grammar tree, now with positions "popped".
   */
  add:x:/../*/p5.web.viewstate.set/*
    src:x:/@p5.web.viewstate.get/*/*
  p5.web.viewstate.set:magic-menu.position
    src

  /*
   * Posting "changed" event.
   */
  magic-menu.position.changed
