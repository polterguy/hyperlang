
/*
 * Creates the event that actually starts Hyperlang.
 */
create-event:hyperlang.start

  /*
   * Checking if Hyperlang is already initiated from before, and if so, making
   * sure we close any previous instances first.
   */
  if
    fetch:x:/0/0?value
      widget-exists:hyperlang-listener
    delete-widget:hyperlang-listener

  /*
   * Making sure we reset grammar position.
   */
  hyperlang.set-position

  /*
   * Creating main Hyperlang widget.
   */
  create-widget:hyperlang-listener
    class:hyperlang-listener shaded rounded air-inner
    oninit

      /*
       * Greets user, and asks for input.
       */
      hyperlang.greet-user


    events

      /*
       * Closes Hyperlang.
       */
      hyperlang.quit

        /*
         * Deletes main Hyperlang widget.
         */
        delete-widget:hyperlang-listener

        /*
         * Says goodbye, and stops input loop.
         */
        hyperlang.goodbye

    widgets
      div
        class:strip
        widgets
          button
            style:"margin-bottom:0;"
            innerValue:@"<span class=""icon-close""></span>"
            title:Close Hyperlang
            oninit

              /*
               * Setting initial focus to "Close" button, to make it easy to close module.
               */
              micro.page.set-focus:x:/../*/_event?value

            onclick

              /*
               * Closing Hyperlang.
               */
              hyperlang.quit

          literal
            element:label
            class:hyperlang-tip
            events

              /*
               * Shows the specified text to the user.
               */
              hyperlang.set-text

                /*
                 * Sets the innerValue of current widget to whatever we were given.
                 */
                set-widget-property:x:/../*/_event?value
                  innerValue:x:/../*/_arg?value

              /*
               * Retrieves the specified text to the caller.
               */
              hyperlang.get-text

                /*
                 * Returns the innerValue of current widget to the caller.
                 */
                get-widget-property:x:/../*/_event?value
                  innerValue:x:/../*/_arg?value
                return:x:/@get-widget-property/*/*?value
