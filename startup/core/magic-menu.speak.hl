
/*
 * Invoked when Magic menu should speak something.
 *
 * Expects [_arg] to be whatever should be spoken, and [onfinish] being what to
 * do once [_arg] has been spoken.
 *
 * Optionally, pass in [voice] to override the default voice to use, which is 
 * fetched from the user's settings.
 *
 * Notice, this event will apply all arguments, except [_arg], into [micro.speak].
 */
create-event:magic-menu.speak

  /*
   * Signal node.
   */
  .signal

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string

  /*
   * If widget was closed, we simply abort.
   */
  if
    fetch:x:/0/0?value
      widget-exists:magic-menu-listener
    not
    return

  /*
   * Unwrapping any references in [_arg] given.
   */
  magic-menu.unwrap-phrase:x:/../*/_arg?value

  /*
   * Checking if speech is enabled, and if not, evaluating any potential
   * [onfinish] lambdas immediately, setting text to whatever was supposed to 
   * be spoken, and returning early.
   */
  if
    magic-menu.speech.is-enabled
    not

    /*
     * Speech is not enabled, updating text, evaluating [onfinish] if existing, 
     * and returning early.
     */
    magic-menu.set-text:x:/@magic-menu.unwrap-phrase?value
    eval:x:/../*/onfinish
    return

  /*
   * Applies arguments, and adds [voice] unless an explicit voice was given as an argument.
   */
  if:x:/../*/voice?value
    not

    /*
     * No voice was given as an argument, making sure we apply the default voice from
     * user's settings.
     */
    magic-menu.settings.get-language
    eval-x:x:/+/*/*
    add:x:/../*/micro.speak
      src
        voice:{0},{1}-{2}
          :x:/@magic-menu.settings.get-language/*/voice?value
          :x:/@magic-menu.settings.get-language/*/lang?value
          :x:/@magic-menu.settings.get-language/*/sublang?value

  /*
   * Applying all arguments given to [micro.speak] except [_arg], which among 
   * other things, also will apply [onfinish].
   */
  add:x:/../*/micro.speak
    src:x:/@.signal/--(!/_arg)

  /*
   * Speaks given [_arg], now "unwrapped" with any references, with the 
   * requested voice.
   */
  micro.speak:x:/@magic-menu.unwrap-phrase?value
