
/*
 * Evaluates the specified [_arg] command.
 *
 * Looks up [_arg] command from database, expecting it to be a "global_id", and evaluates it.
 * Notice, this does not validate that the command belong to the user or anything like that, but
 * expects the caller to have asserted that before invoking this event.
 */
create-event:magic-menu.command.evaluate

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string

  /*
   * Stopping speech engine.
   */
  micro.speech.stop

  /*
   * Deletes the visual clue, indicating we're listening for input.
   */
  if
    fetch:x:/0/0?value
      widget-exists:magic-menu-listener

    /*
     * Magic Menu exists on page.
     */
    micro.css.delete:magic-menu-listener
      class:magic-menu-listening

  /*
   * Looks up command from database.
   */
  p5.mysql.connect:[magic-menu]

    /*
     * Evaluating parametrised SQL now.
     *
     * Making sure we prioritise phrases in "current level", and making sure we
     * only select one match.
     */
    p5.mysql.select:@"select id, hyperlambda from commands where global_id = @global_id"
      @global_id:x:/../*/_arg?value

    /*
     * Checking to see if we actually found a command.
     */
    if:x:/@p5.mysql.select/*

      /*
       * Converting Hyperlambda to lambda, and evaluating it.
       *
       * Notice, we pass in [jump] to signal to lambda that this was a "jump" invocation.
       */
      hyper2lambda:x:/@p5.mysql.select/*/*/hyperlambda?value
      eval:x:/@hyper2lambda
        jump:bool:true

      /*
       * Checking if we should "push" current ID unto our "grammar-position" stack.
       *
       * This will only be done, if lambda for command returns true.
       */
      if:x:/@eval?value
        !=:bool:false

        /*
         * Notice, if lambda returns "false", we do not push the ID of the current
         * command unto our stack.
         */
        magic-menu.grammar-position.push:x:/@p5.mysql.select/*/*/id?value

    else

      /*
       * Command didn't exist.
       */
      magic-menu.listen:[I don't understand]
