/*
 * Invoked when Magic menu should listen for new input.
 *
 * Optionally pass in [_arg] being something to speak before listening is initiated,
 * and an [onfinish] lambda, to be invoked after something has been spoken.
 *
 * The default [onfinish], if none is given, will simply try to "handle" the spoken
 * phrase as a command.
 *
 * You can also optionally pass in [lang], to override the language to listen for.
 */
create-event:magic-menu.listen

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.optional:x:/..
    onfinish
    _arg:string
    lang:string
    voice:string

  /*
   * Applying [onfinish], if any.
   */
  add:x:/../**/magic-menu._listen
    src:x:(/../*/onfinish|/../*/lang)

  /*
   * Checks to see if caller passed in [_arg], at which point we speak the given [_arg],
   * before we start listening.
   */
  if:x:/../*/_arg?value

    /*
     * Speaking given [_arg], before we start listening.
     */
    add:x:/+
      src:x:/../*/voice
    magic-menu.speak:x:/../*/_arg?value
      onfinish

        /*
         * Invoking event responsible for starting listening process.
         */
        magic-menu._listen

  else

    /*
     * Invoking event responsible for starting listening process.
     */
    magic-menu._listen
