
/*
 * Creates the event that actually starts Hyperlang.
 *
 * You can override the default greeting with [greeting]
 */
create-event:hyperlang.start

  /*
   * Defaults.
   */
  .defaults
    greeting:[Hello]

  /*
   * Checking if Hyperlang is already initiated from before, and if so, making
   * sure we close any previous instances first.
   */
  if
    fetch:x:/0/0?value
      widget-exists:hyperlang-listener
    delete-widget:hyperlang-listener

  /*
   * Making sure we reset grammar position.
   */
  hyperlang.grammar-position.clear

  /*
   * Creating main Hyperlang widget.
   */
  create-widget:hyperlang-listener
    class:hyperlang-listener shaded rounded air-inner
    widgets
      div
        class:strip
        widgets
          button
            style:"margin-bottom:0;"
            title:Pauses temporarily
            events

              /*
               * Returns true if speech is enabled, otherwise false.
               */
              hyperlang.speech-is-enabled

                /*
                 * Checking if speech is enabled.
                 */
                p5.web.widgets.get-children:x:/../*/_event?value
                get-widget-property:x:/-/*/*?value
                  class
                if:x:/@get-widget-property/*/*?value
                  =:icon-pause2

                  /*
                   * Speech is enabled.
                   */
                  return:bool:true

                else

                  /*
                   * Speech is disabled.
                   */
                  return:bool:false

            onclick

              /*
               * Figuring out if we should pause or restart.
               */
              p5.web.widgets.get-children:x:/../*/_event?value
              get-widget-property:x:/-/*/*?value
                class
              if:x:/@get-widget-property/*/*?value
                =:icon-pause2

                /*
                 * We should temporarily pause input loop.
                 * Deletes the visual clue, indicating we're listening for input,
                 * and makes sure we stop listening and speaking.
                 */
                micro.css.delete:hyperlang-listener
                  class:hyperlang-listening
                micro.listen.stop
                micro.speak.stop
                set-widget-property:x:/@p5.web.widgets.get-children/*/*?value
                  class:icon-play3

                /*
                 * Notice, we also store in our p5.data database if speech is 
                 * disabled for user.
                 */
                whoami
                delete-data:x:/*/*/hyperlang.speech-is-disabled-{0}
                  :x:/@whoami/*/username?value
                set:x:/+/*?name
                  src:hyperlang.speech-is-disabled-{0}
                    :x:/@whoami/*/username?value
                insert-data
                  foo:bool:true
                  

              else

                /*
                 * We should restart input loop.
                 */
                set-widget-property:x:/@p5.web.widgets.get-children/*/*?value
                  class:icon-pause2
                hyperlang.listen:[Talk to me]

                /*
                 * Notice, we also store in our p5.data database if speech is 
                 * disabled for user.
                 */
                whoami
                delete-data:x:/*/*/hyperlang.speech-is-disabled-{0}
                  :x:/@whoami/*/username?value

            oninit

              /*
               * Notice, we also store in our p5.data database if speech is 
               * disabled for user.
               */
              whoami
              select-data:x:/*/*/hyperlang.speech-is-disabled-{0}
                :x:/@whoami/*/username?value
              if:x:/@select-data/*?value

                /*
                 * Speech is by default disabled for current session.
                 */
                micro.css.delete:hyperlang-listener
                  class:hyperlang-listening
                p5.web.widgets.get-children:x:/../*/_event?value
                set-widget-property:x:/@p5.web.widgets.get-children/*/*?value
                  class:icon-play3

                /*
                 * When speech is by default disabled for user, we automatically
                 * show our options, since no other input elements exists.
                 */
                hyperlang.options.show

            widgets
              literal
                element:span
                class:icon-pause2

          literal:hyperlang-current-text
            element:label
            class:hyperlang-tip
            visible:false

          button
            style:"margin-bottom:0;"
            innerValue:@"<span class=""icon-plus""></span>"
            title:Teach computer phrase
            visible:false
            events

              /*
               * Event that displays our "create-command" button.
               */
              hyperlang.display-create-command-button

                /*
                 * Simply making widget visible.
                 */
                set-widget-property:x:/../*/_event?value
                  visible:true
                  .spoken:x:/../*/_arg?value

              /*
               * Event that hides our "create-command" button.
               */
              hyperlang.hide-create-command-button

                /*
                 * Simply making widget visible.
                 */
                set-widget-property:x:/../*/_event?value
                  visible:false

            onclick

              /*
               * Invokes event responsible for creating a new association, making
               * sure we pass in proper arguments.
               */
              get-widget-property:x:/../*/_event?value
                .spoken
              hyperlang.grammar-position.get
              if:x:/@hyperlang.grammar-position.get?value
                eval-x:x:/+/*/*
                add:x:/../*/hyperlang.command.create
                  src
                    pos:x:/@hyperlang.grammar-position.get?value
              hyperlang.command.create:x:/@get-widget-property/*/*?value

              /*
               * Deletes the visual clue, indicating we're listening for input,
               * and makes sure we stop listening and speaking.
               */
              micro.css.delete:hyperlang-listener
                class:hyperlang-listening
              micro.listen.stop
              micro.speak.stop

          button
            style:"margin-bottom:0;"
            innerValue:@"<span class=""icon-question""></span>"
            title:Show options
            onclick

              /*
               * Checking if widget exists already, and if so, we hide it.
               */
              if
                fetch:x:/0/0?value
                  widget-exists:hyperlang-help

                /*
                 * Hides options for Hyperlang, and deletes CSS "toggled" class
                 * for button.
                 */
                hyperlang.options.close
                micro.css.delete:x:/../*/_event?value
                  class:toggled

              else

                /*
                 * Showing options for Hyperlang, and adds CSS "toggled" class
                 * for button.
                 */
                hyperlang.options.show
                micro.css.add:x:/../*/_event?value
                  class:toggled

          button
            style:"margin-bottom:0;"
            innerValue:@"<span class=""icon-cross""></span>"
            title:Close Hyperlang
            accesskey:Q
            onclick

              /*
               * Closing Hyperlang.
               */
              hyperlang.stop

  /*
   * Greets user, and asks for input.
   */
  hyperlang.listen:x:(/../*/greeting|/@.defaults/*/greeting)/$?value
