
/*
 * Creates the event that allows the user to configure a phrase to a piece of
 * Hyperlambda.
 *
 * Expects [_arg] to be phrase to create association for.
 */
create-event:hyperlanguage.create-phrase2hyper-association

  /*
   * Creating a modal widget, which asks user to type in Hyperlambda, and configure
   * a piece of Hyperlambda, which will result in associating the specified Hyperlambda
   * with the given [_arg] phrase.
   */
  eval-x:x:/+/*/*/*/h3/*/innerValue
  create-widgets
    micro.widgets.modal:hyperlanguage-create-phrase2hyper-association
      widgets
        h3
          innerValue:Configure <em>'{0}'</em>
            :x:/../*/_arg?value
        label
          innerValue:Synonymous phrases
          for:hyperlanguage-synonymous-phrases
          style:"margin-bottom:0;"
        literal:hyperlanguage-synonymous-phrases
          element:textarea
          class:fill
          rows:5
          placeholder:Type in synonymous phrases here, one for each line ...
          .data-field:synonyms
          value:x:/../*/_arg?value
        label
          innerValue:Hyperlambda to asociate with phrase(s)
          style:"margin-bottom:0;"
        hypereval.widgets.hyperlambda-textarea:hyper-input
          element:textarea
          .data-field:hyperlambda
        label
          title:Is this a global command, implying something you could tell Elizabeth at any time?
          widgets
            input
              type:checkbox
              .data-field:global
            span
              innerValue:Global
        label
          title:Should this phrase only apply for you?
          widgets
            input
              type:checkbox
              checked
              .data-field:private
            span
              innerValue:Private phrase
        div
          class:right
          widgets
            div
              class:strip
              style:"display:inline-block;"
              widgets
                button
                  innerValue:OK
                  style:"margin-bottom:0;"
                  onclick

                    /*
                     * Retrieving code from CodeMirror widget, and creating an
                     * association between phrase and code, before we close widget
                     * and restarts listening loop.
                     */
                    micro.form.serialize:hyperlanguage-create-phrase2hyper-association

                    /*
                     * Connecting to database, and inserting into "commands" and "phrases".
                     */
                    p5.mysql.connect:[hyperlanguage]

                      /*
                       * Retrieving current position in grammar tree.
                       */
                      hyperlanguage.get-position

                      /*
                       * Checking if this is a "private" command, at which point we
                       * insert it with the currently logged in user's username.
                       */
                      if:x:/@micro.form.serialize/*/private?value

                        /*
                         * Private command, checking if it's global.
                         */
                        whoami
                        if:x:/@micro.form.serialize/*/global?value

                          /*
                           * Global command, implying no "parent" relationship.
                           */
                          p5.mysql.insert:@"insert into commands (hyperlambda, username, global) values (@hyperlambda, @username, 1)"
                            @hyperlambda:x:/@micro.form.serialize/*/hyperlambda?value
                            @username:x:/@whoami/*/username?value

                        else

                          /*
                           * Not a global command, inserting also "parent"
                           */
                          p5.mysql.insert:@"insert into commands (hyperlambda, username, parent) values (@hyperlambda, @username, @parent)"
                            @hyperlambda:x:/@micro.form.serialize/*/hyperlambda?value
                            @username:x:/@whoami/*/username?value
                            @parent:x:/@hyperlanguage.get-position?value

                      else

                        /*
                         * Public command, checking if it's global.
                         */
                        if:x:/@micro.form.serialize/*/global?value

                          /*
                           * Global command, implying no "parent" relationship.
                           */
                          p5.mysql.insert:@"insert into commands (hyperlambda, global) values (@hyperlambda, 1)"
                            @hyperlambda:x:/@micro.form.serialize/*/hyperlambda?value

                        else

                          /*
                           * Not a global command, inserting also "parent"
                           */
                          p5.mysql.insert:@"insert into commands (hyperlambda, parent) values (@hyperlambda, @parent)"
                            @hyperlambda:x:/@micro.form.serialize/*/hyperlambda?value
                            @parent:x:/@hyperlanguage.get-position?value

                      /*
                       * Splitting synonyms, and inserting one 'phrase' for each.
                       */
                      split:x:/@micro.form.serialize/*/synonyms?value
                        =:"\r\n"
                      for-each:x:/@split/*?name
                        p5.mysql.insert:@"insert into phrases (command, commandid) values (@command, @commandid)"
                          @command:x:/@_dp?value
                          @commandid:x:/..p5.mysql.connect/**/p5.mysql.insert/*/id/[0,1]?value

                    /*
                     * Closing modal widget, and restarting listening loop.
                     */
                    delete-widget:hyperlanguage-create-phrase2hyper-association
                    hyperlanguage.speak:OK
                      onfinish

                        /*
                         * Restarting input loop.
                         */
                        hyperlanguage.listen

                button
                  innerValue:Cancel
                  style:"margin-bottom:0;"
                  onclick

                    /*
                     * Closing modal widget, and restarting listening loop.
                     */
                    delete-widget:hyperlanguage-create-phrase2hyper-association
                    hyperlanguage.speak:Talk to me
                      onfinish

                        /*
                         * Restarting input loop.
                         */
                        hyperlanguage.listen
