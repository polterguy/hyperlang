
/*
 * Invoked when Hyperlang should speak something.
 *
 * Expects [_arg] to be whatever should be spoken.
 */
create-event:hyperlang.speak

  /*
   * Checking if this is a referenced word, implying a word that should be translated.
   */
  if
    starts-with:x:/../*/_arg?value
      src:[
    and
      ends-with:x:/../*/_arg?value
        src:]

    /*
     * Referenced word, doing lookup into language files, and changing spoken
     * phrase accordingly.
     */
    load-file:@HYPERLANG/configuration/lang-en.hl
    set:x:/../*/_arg?value
      src:x:/@load-file/*/*/\{0}?value
        :x:/../*/_arg?value

  /*
   * Signal node, to separate arguments from the rest of our lambda.
   */
  .signal

  /*
   * Applies arguments.
   */
  add:x:/../*/micro.speak
    src:x:/@.signal/--(!/_arg)

  /*
   * Making sure we remove "active" CSS class when speech recognition is done.
   */
  if
    fetch:x:/0/0?value
      widget-exists:hyperlang-listener
    micro.css.delete:hyperlang-listener
      class:hyperlang-listening

  /*
   * Speaks given [_arg] with our configured voice.
   */
  micro.speak:x:/../*/_arg?value
    voice:Karen,en
