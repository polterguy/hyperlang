
/*
 * Invoked when Hyperlang should speak something.
 *
 * Expects [_arg] to be whatever should be spoken.
 */
create-event:hyperlang.speak

  /*
   * Defaults, in case no settings exists.
   */
  .defaults
    voice:Karen,en

  /*
   * Retrieving settings for user, to figure out voice to use, etc.
   */
  p5.auth.my-settings.get

  /*
   * Checking if this is a referenced word, implying a word that should be translated.
   */
  if
    starts-with:x:/../*/_arg?value
      src:[
    and
      ends-with:x:/../*/_arg?value
        src:]

    /*
     * Referenced word, doing a lookup into language file, and changing spoken
     * phrase accordingly.
     */
    split:x:(/@p5.auth.my-settings.get/*/hyperlang/*/voice|/@.defaults/*/voice)/$?value
      =:,
    load-file:@HYPERLANG/configuration/lang-{0}.hl
      :x:/@split/0/-?name
    set:x:/../*/_arg?value
      src:x:/@load-file/*/*/\{0}?value
        :x:/../*/_arg?value

  /*
   * Signal node, to separate arguments from the rest of our lambda.
   */
  .signal

  /*
   * Applies arguments.
   */
  add:x:/../*/micro.speak
    src:x:/@.signal/--(!/_arg)

  /*
   * Making sure we remove "active" CSS class when speech recognition is done.
   */
  if
    fetch:x:/0/0?value
      widget-exists:hyperlang-listener
    micro.css.delete:hyperlang-listener
      class:hyperlang-listening

  /*
   * Speaks given [_arg] with our configured voice.
   */
  eval-x:x:/+/*
  micro.speak:x:/../*/_arg?value
    voice:x:(/@p5.auth.my-settings.get/*/hyperlang/*/voice|/@.defaults/*/voice)/$?value
