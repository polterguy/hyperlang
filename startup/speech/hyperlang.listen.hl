
/*
 * Invoked when Hyperlang should listen for new input.
 *
 * Optionally pass in [_arg] being something to speak before listening is initiated,
 * and a [onfinish] lambda, to beinvoked after something has been recognized.
 */
create-event:hyperlang.listen

  /*
   * Defaults.
   */
  .defaults
    lang:en
    onfinish

      /*
       * Invokes event responsible for handling input.
       */
      hyperlang.handle-input:x:/../*/text?value

  /*
   * Retrieving settings for user, to figure out language to listen for, etc.
   */
  p5.auth.my-settings.get
  if:x:/@p5.auth.my-settings.get/*/hyperlang/*/voice?value
    split:x:/@p5.auth.my-settings.get/*/hyperlang/*/voice?value
      =:,
    set:x:/@.defaults/*/lang?value
      src:x:/@split/0/-?name

  /*
   * Applying [onfinish], or default value if none was given.
   */
  add:x:/../**/micro.listen/*/onfinish/*/try
    src:x:(/../*/onfinish|/@.defaults/*/onfinish)/$/*

  /*
   * Checks to see if caller passed in [_arg], at which point we speak the given [_arg],
   * before we initiate listening.
   */
  eval-x:x:/../**/micro.listen/*/lang
  if:x:/../*/_arg?value

    /*
     * Speaking given [_arg], before we start listening.
     */
    hyperlang.speak:x:/../*/_arg?value
      onfinish

        /*
         * Making sure we add "active" CSS class when speech recognition is initiated.
         */
        micro.css.add:hyperlang-listener
          class:hyperlang-listening

        /*
         * Start listening.
         */
        micro.listen
          lang:x:/@.defaults/*/lang?value
          onfinish
            try

              /*
               * Will be dynamically populated above.
               */

            catch

              /*
               * Giving feedback to user.
               */
              hyperlang.listen:An exception occurred
              micro.windows.info:x:/..catch/*/message?value
                class:micro-windows-info warning

  else

    /*
     * Making sure we add "active" CSS class when speech recognition is initiated.
     */
    micro.css.add:hyperlang-listener
      class:hyperlang-listening

    /*
     * Starts speech recognition engine directly.
     */
    micro.listen
      lang:x:/@.defaults/*/lang?value
      onfinish
        try

          /*
           * Will be dynamically populated above.
           */

        catch

          /*
           * Giving feedback to user.
           */
          hyperlang.listen:An exception occurred
          micro.windows.info:x:/..catch/*/message?value
            class:micro-windows-info warning
