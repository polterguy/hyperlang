
/*
 * Creates the event that actually starts Hyperlanguage.
 */
create-event:hyperlanguage.start

  /*
   * Checking if Hyperlanguage is already initiated from before, and if so, making
   * sure we close any previous instances first.
   */
  if
    fetch:x:/0/0?value
      widget-exists:hyperlanguage-listener
    delete-widget:hyperlanguage-listener

  /*
   * Creating main Hyperlanguage widget.
   */
  create-widget:hyperlanguage-listener
    class:hyperlanguage-listener shaded rounded air-inner
    oninit

      /*
       * Greets user, and asks for input.
       */
      hyperlanguage.greet-user


    events

      /*
       * Closes Hyperlanguage.
       */
      hyperlanguage.actions.close

        /*
         * Deletes main Hyperlanguage widget.
         */
        delete-widget:hyperlanguage-listener

        /*
         * Says goodbye, and stops input loop.
         */
        hyperlanguage.goodbye

    widgets
      div
        class:strip
        widgets
          button
            style:"margin-bottom:0;"
            innerValue:@"<span class=""icon-close""></span>"
            title:Close Hyperlanguage
            oninit

              /*
               * Setting initial focus to "Close" button, to make it easy to close module.
               */
              micro.page.set-focus:x:/../*/_event?value

            onclick

              /*
               * Closing Hyperlanguage.
               */
              hyperlanguage.actions.close

          literal
            element:label
            class:hyperlanguage-tip
            events

              /*
               * Shows the specified text to the user.
               */
              hyperlanguage.set-text

                /*
                 * Sets the innerValue of current widget to whatever we were given.
                 */
                set-widget-property:x:/../*/_event?value
                  innerValue:x:/../*/_arg?value

              /*
               * Retrieves the specified text to the caller.
               */
              hyperlanguage.get-text

                /*
                 * Returns the innerValue of current widget to the caller.
                 */
                get-widget-property:x:/../*/_event?value
                  innerValue:x:/../*/_arg?value
                return:x:/@get-widget-property/*/*?value
