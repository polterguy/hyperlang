/*
 * Creates the event that sets the "current text" (tip).
 *
 * Expects [_arg] being the new text to display.
 */
create-event:magic-menu.set-text

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.optional:x:/..
    _arg:string

  /*
   * Verifying widget actually exists before we attempt to change its value.
   */
  if
    fetch:x:/0/0?value
      widget-exists:magic-menu-current-text

    /*
     * Checking if we were given an actual text value, and if not, 
     * making text widget invisible.
     */
    if:x:/../*/_arg?value
      and:x:/../*/_arg?value
        !=:

      /*
       * Unwrapping any references in [_arg] given.
       */
      magic-menu.unwrap-phrase:x:/../*/_arg?value

      /*
       * Checking if text is too long to display, at which point we cut it.
       */
      if
        length:x:/@magic-menu.unwrap-phrase?value
        >:int:40
        split:x:/@magic-menu.unwrap-phrase?value
          =:int:40
        set-widget-property:magic-menu-current-text
          title:x:/@magic-menu.unwrap-phrase?value
        set:x:/@magic-menu.unwrap-phrase?value
          src:{0} ...
            :x:/@split/0?name
      else
        delete-widget-property:magic-menu-current-text
          title

      /*
       * Text was given.
       */
      set-widget-property:magic-menu-current-text
        innerValue:x:/@magic-menu.unwrap-phrase?value
        visible:true

    else

      /*
       * Text was not given.
       */
      set-widget-property:magic-menu-current-text
        visible:false

    /*
     * Returning success to caller.
     */
    return:bool:true

  /*
   * Returning failure to caller.
   */
  return:bool:false
