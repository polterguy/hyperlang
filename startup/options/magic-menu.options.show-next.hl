
/*
 * Creates the event that shows the next page of available options to user.
 */
create-event:magic-menu.options.show-next

  /*
   * Defaults.
   */
  .defaults
    magic-menu.options.offset:long:0

  /*
   * Ensuring that "options" window is created.
   */
  magic-menu.options._ensure-window

  /*
   * Checking if we have a filter condition.
   */
  magic-menu.options.get-filter
  if:x:/@magic-menu.options.get-filter?value
    add:x:/../*/magic-menu.grammar-position.list-options
      src
        filter:x:/@magic-menu.options.get-filter?value

  /*
   * Increments current "page" for options.
   */
  magic-menu.settings.get:no-options
    default:5
  p5.web.viewstate.get:magic-menu.options.offset
  +:x:(/@p5.web.viewstate.get/*/magic-menu.options.offset|/@.defaults/*/magic-menu.options.offset)/$?value.long
    _:x:/@magic-menu.settings.get?value

  /*
   * Retrieving options with offset incremented now.
   */
  eval-x:x:/+/*
  magic-menu.grammar-position.list-options
    limit:x:/@magic-menu.settings.get?value
    offset:x:/@+?value.long

  /*
   * Checking if we should disable or enable the next button.
   */
  delete-widget-property:magic-menu-options-page-previous
    disabled
  if:x:/@magic-menu.grammar-position.list-options/*?count
    <:x:/@magic-menu.settings.get?value.int

    /*
     * No more next items.
     */
    set-widget-property:magic-menu-options-page-next
      disabled

  /*
   *
   */
  if:x:/@magic-menu.grammar-position.list-options/*?count
    =:int:0

    /*
     * No more options.
     */
    return:bool:false

  /*
   * We have more options, first updating "current page" value.
   */
  p5.web.viewstate.set:magic-menu.options.offset
    src:x:/@+?value

  /*
   * Parametrising and invoking event responsible for "data binding" options window.
   */
  add:x:/../*/magic-menu.options._create-options/*/options
    src:x:/@magic-menu.grammar-position.list-options/*
  magic-menu.options._create-options
    parent:magic-menu-help-list
    options

  /*
   * Making sure we initialize options.
   */
  eval-x:x:/+/*
  magic-menu.options._initialize:x:/@magic-menu.grammar-position.list-options/*?count
    offset:x:/@+?value.int

  /*
   * Returning success to caller.
   */
  return:bool:true
