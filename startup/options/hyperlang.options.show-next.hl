
/*
 * Creates the event that shows the next page of available options to user.
 */
create-event:hyperlang.options.show-next

  /*
   * Defaults.
   */
  .defaults
    hyperlang.options.offset:long:0

  /*
   * Ensuring that "options" window is created.
   */
  hyperlang.options._ensure-window

  /*
   * Checking if we have a filter condition.
   */
  hyperlang.options.get-filter
  if:x:/@hyperlang.options.get-filter?value
    add:x:/../*/hyperlang.grammar-position.list-options
      src
        filter:x:/@hyperlang.options.get-filter?value

  /*
   * Increments current "page" for options.
   */
  hyperlang.settings.get:no-options
    default:10
  p5.web.viewstate.get:hyperlang.options.offset
  +:x:(/@p5.web.viewstate.get/*/hyperlang.options.offset|/@.defaults/*/hyperlang.options.offset)/$?value.long
    _:x:/@hyperlang.settings.get?value

  /*
   * Retrieving options with offset incremented now.
   */
  eval-x:x:/+/*
  hyperlang.grammar-position.list-options
    limit:x:/@hyperlang.settings.get?value
    offset:x:/@+?value.long

  /*
   *
   */
  if:x:/@hyperlang.grammar-position.list-options/*?count
    =:int:0

    /*
     * No more options.
     */
    return:bool:false

  /*
   * We have more options, first updating "current page" value.
   */
  p5.web.viewstate.set:hyperlang.options.offset
    src:x:/@+?value

  /*
   * Parametrising and invoking event responsible for "data binding" options window.
   */
  add:x:/../*/hyperlang.options._create-options/*/options
    src:x:/@hyperlang.grammar-position.list-options/*
  hyperlang.options._create-options
    parent:hyperlang-help-list
    options

  /*
   * Returning success to caller.
   */
  return:bool:true
