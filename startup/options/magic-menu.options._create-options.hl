
/*
 * Internal event for creating options elements.
 *
 * Requires [parent] and [options] being collection of options to display.
 * Options needs to be phrases' database ID as value, and text to display as
 * value.
 *
 * Optionally pass in boolean [onclick] to have a custom onclick event occur 
 * when item is clicked, instead of the default, which will raise the command
 * associated with the phrases' ID.
 *
 * Notice, this event is used internally to create both "default options", in
 * addition to "custom options".
 */
create-event:magic-menu.options._create-options

  /*
   * Default values.
   */
  .defaults
    onclick

      /*
       * Invokes command with specified commands ID, making sure we stop speech
       * engine first, to avoid "race conditions".
       */
      get-widget-property:x:/../*/_event?value
        .id

      /*
       * Evaluating command.
       */
      eval-x:x:/+/*
      magic-menu.command.handle
        id:x:/@get-widget-property/*/*?value

      /*
       * Removing filter, if there is any.
       *
       * Notice, since we do this after evaluating command, the command can actually
       * use the filter value for something, if it wants to.
       */
      magic-menu.options.set-filter

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    parent:string
    options
  micro.lambda.contract.optional:x:/..
    onclick

  /*
   * Clears previous "li" elements.
   */
  clear-widget:x:/../*/parent?value

  /*
   * Looping through each return value from above invocation.
   *
   * However, first we pass in our [onclick] event to our [create-widget]
   * invocation.
   */
  add:x:/../*/for-each/*/create-widget/*/widgets/*/a
    src:x:(/../*/onclick|/@.defaults/*/onclick)/$
  for-each:x:/../*/options/*

    /*
     * Creating an [onclick] event for element(s).
     */
    create-widget
      element:li
      parent:x:/../*/parent?value
      widgets
        a
          href:#
          role:button
          innerValue:x:/@_dp/#?value
          .id:x:/@_dp/#?name
          class:x:/@_dp/#/*/type?value
