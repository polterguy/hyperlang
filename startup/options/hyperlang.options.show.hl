
/*
 * Creates the event that shows the available options to user.
 */
create-event:hyperlang.options.show

  /*
   * Ensuring that "options" window is created.
   */
  hyperlang.options._ensure-window

  /*
   * Clearing out "current page" viewstate value.
   */
  p5.web.viewstate.set:hyperlang.options.offset
    src:0

  /*
   * Checking if we have a filter condition.
   */
  hyperlang.options.get-filter
  if:x:/@hyperlang.options.get-filter?value
    add:x:/../*/hyperlang.grammar-position.list-options
      src
        filter:x:/@hyperlang.options.get-filter?value

  /*
   * Retrieves the first "n" options.
   */
  hyperlang.settings.get:no-options
    default:10
  eval-x:x:/+/*
  hyperlang.grammar-position.list-options
    limit:x:/@hyperlang.settings.get?value

  /*
   * Parametrising and invoking event responsible for "data binding" options window.
   */
  add:x:/../*/hyperlang.options._create-options/*/options
    src:x:/@hyperlang.grammar-position.list-options/*
  hyperlang.options._create-options
    parent:hyperlang-help-list
    options

  /*
   * Checking if there are any options.
   */
  if:x:/@hyperlang.grammar-position.list-options/*?count
    =:int:0

    /*
     * No options, returning "failure" to caller.
     */
    return:bool:false

  /*
   * Returning "success" to caller.
   */
  return:bool:true