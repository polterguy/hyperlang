
/*
 * Creates the event that shows the previous page of available options to user.
 */
create-event:magic-menu.options.show-previous

  /*
   * Ensuring that "options" window is created.
   */
  magic-menu.options._ensure-window

  /*
   * Retrieves current "page" for options.
   */
  magic-menu.options.get-offset

  /*
   * Checking if we have no more previous options.
   */
  if:x:/@magic-menu.options.get-offset?value.int
    =:int:0

    /*
     * We're at the beginning of our options dataset, returning false to caller,
     * to signal "failure".
     */
    return:bool:false

  /*
   * Checking if we have a filter condition.
   */
  magic-menu.options.get-filter
  if:x:/@magic-menu.options.get-filter?value
    add:x:/../*/magic-menu.grammar-position.list-options
      src
        filter:x:/@magic-menu.options.get-filter?value

  /*
   * Decrements current page.
   */
  magic-menu.settings.get:no-options
    default:5
  -:x:/@magic-menu.options.get-offset?value.long
    _:x:/@magic-menu.settings.get?value

  /*
   * Retrieving options with offset incremented now.
   */
  eval-x:x:/+/*
  magic-menu.grammar-position.list-options
    limit:x:/@magic-menu.settings.get?value
    offset:x:/@-?value.long

  /*
   * Updating "current page" value.
   */
  magic-menu.options.set-offset:x:/@-?value.int

  /*
   * Parametrising and invoking event responsible for "data binding" options window.
   */
  add:x:/../*/magic-menu.options._create-options/*/options
    src:x:/@magic-menu.grammar-position.list-options/*
  magic-menu.options._create-options
    parent:magic-menu-help-list
    options

  /*
   * Making sure we initialize options.
   */
  eval-x:x:/+/*
  magic-menu.options._initialize:x:/@magic-menu.grammar-position.list-options/*?count
    offset:x:/@-?value.int

  /*
   * Returning success to caller.
   */
  return:bool:true
