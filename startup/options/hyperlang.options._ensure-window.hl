
/*
 * Private event that ensures that our common "options" window is created.
 */
create-event:hyperlang.options._ensure-window

  /*
   * Checking if widget exists already, and if so, returning early.
   */
  if
    fetch:x:/0/0?value
      widget-exists:hyperlang-help
    return

  /*
   * Creates common helper window, that displays options.
   */
  create-widget:hyperlang-help
    class:hyperlang-help rounded shaded bg
    widgets
      div
        class:container
        widgets
          div
            class:row
            widgets
              div
                class:col
                widgets
                  div
                    class:strip fill hyperlang-options-pager-toolbar
                    widgets
                      input:hyperlang-options-search-textbox
                        type:text
                        placeholder:Search ...
                        class:smaller
                        oninit

                          /*
                           * Clears the current filter.
                           */
                          p5.web.viewstate.set:hyperlang.options-filter

                        events

                          /*
                           * Returns the current filter to caller.
                           */
                          hyperlang.options.get-filter

                            /*
                             * Returning current filter to caller, if any.
                             */
                            p5.web.viewstate.get:hyperlang.options-filter
                            return:x:/@p5.web.viewstate.get/*?value

                        onkeydown:@"if (event.keyCode == 13) {p5.$('hyperlang-options-search-textbox').raise('.onsearch');return false;}"
                        .onsearch

                          /*
                           * Making sure we store current filter in ViewState
                           */
                          get-widget-property:hyperlang-options-search-textbox
                            value
                          if:x:/@get-widget-property/*/*?value
                            =:

                            /*
                             * Filter was cleared.
                             */
                            p5.web.viewstate.set:hyperlang.options-filter
                            set-widget-property:hyperlang-options-search-clear
                              disabled

                          else

                            /*
                             * Some filter was supplied.
                             */
                            p5.web.viewstate.set:hyperlang.options-filter
                              src:x:/@get-widget-property/*/*?value
                            delete-widget-property:hyperlang-options-search-clear
                              disabled

                          /*
                           * Reloading options, now with filter (or with filter removed).
                           *
                           * Notice, if filter yields zero results, we assume user wants to
                           * possibly create a new command, and hence we enable our "create new
                           * command button".
                           */
                          if
                            hyperlang.options.show

                            /*
                             * Disabling "create new command button".
                             */
                            hyperlang.hide-create-command-button

                          else

                            /*
                             * Enabling "create new command button".
                             */
                            hyperlang.set-text:x:/@get-widget-property/*/*?value
                            hyperlang.display-create-command-button:x:/@get-widget-property/*/*?value

                          micro.page.set-focus:x:/../*/_event?value

                      button:hyperlang-options-search-clear
                        innerValue:@"<span class=""icon-cross""></span>"
                        disabled
                        onclick

                          /*
                           * Removes current filter, and re-databinds options.
                           */
                          p5.web.viewstate.set:hyperlang.options-filter
                          hyperlang.options.show
                          set-widget-property:x:/../*/_event?value
                            disabled
                          set-widget-property:hyperlang-options-search-textbox
                            value:
                          micro.page.set-focus:hyperlang-options-search-textbox
                          hyperlang.hide-create-command-button

                      button
                        innerValue:@"<span class=""icon-arrow-left""></span>"
                        onclick

                          /*
                           * Paging to previous options.
                           */
                          hyperlang.options.show-previous

                      button
                        innerValue:@"<span class=""icon-arrow-right""></span>"
                        onclick

                          /*
                           * Paging to next options.
                           */
                          hyperlang.options.show-next

                  container:hyperlang-help-list-custom
                    element:ul
                    class:hyperlang-custom-options
                    events

                      /*
                       * Invoked when current options have changed.
                       */
                      hyperlang.position.changed

                        /*
                         * Simply re-displaying options will do the trick for us here.
                         */
                        hyperlang.options.show

                      /*
                       * Invoked when options should be refreshed for some reasons.
                       */
                      hyperlang.options.changed

                        /*
                         * Simply re-displaying options will do the trick for us here.
                         */
                        hyperlang.options.show

                      /*
                       * Helper active event to clear custom options
                       */
                      hyperlang.options.clear-custom

                        /*
                         * Clearing custom options.
                         */
                        clear-widget:x:/../*/_event?value

                  container:hyperlang-help-list
                    element:ul
                    class:hyperlang-options
