
/*
 * Private event that ensures that our common "options" window is created.
 */
create-event:hyperlang.options._ensure-window

  /*
   * Checking if widget exists already, and if so, returning early.
   */
  if
    fetch:x:/0/0?value
      widget-exists:hyperlang-help
    return

  /*
   * Creates common helper window, that displays options.
   */
  create-widget:hyperlang-help
    class:hyperlang-help rounded shaded bg
    widgets
      div
        class:container
        widgets
          div
            class:row
            widgets
              div
                class:col
                widgets
                  literal
                    class:hyperlang-help-close
                    element:a
                    href:#
                    innerValue:@"<span class=""icon-close""></span>"
                    onclick

                      /*
                       * Closing widget.
                       */
                      hyperlang.options.close

                  container:hyperlang-help-list-custom
                    element:ul
                    class:hyperlang-custom-options
                    events

                      /*
                       * Invoked when current options have changed.
                       */
                      hyperlang.position.changed

                        /*
                         * Simply re-displaying options will do the trick for us here.
                         */
                        hyperlang.options.show

                      /*
                       * Helper active event to clear custom options
                       */
                      hyperlang.options.clear-custom

                        /*
                         * Clearing custom options.
                         */
                        clear-widget:x:/../*/_event?value

                  container:hyperlang-help-list
                    element:ul
                    class:hyperlang-options
                  div
                    class:right
                    widgets
                      a
                        href:#
                        role:button
                        class:hyperlang-help-pager
                        innerValue:@"<span class=""icon-chevron-left""></span>"
                        onclick

                          /*
                           * Paging to previous options.
                           */
                          if
                            hyperlang.options.show-previous
                            not
                            micro.windows.info:There are no more previous options
                              class:micro-windows-info warning

                      a
                        href:#
                        role:button
                        class:hyperlang-help-pager
                        innerValue:@"<span class=""icon-chevron-right""></span>"
                        onclick

                          /*
                           * Paging to next options.
                           */
                          if
                            hyperlang.options.show-next
                            not
                            micro.windows.info:There are no more options
                              class:micro-windows-info warning
