
/*
 * Returns the localized specified phrase.
 */
create-event:hyperlang.language.get-localized-phrase

  /*
   * Retrieving settings for user, to figure out language to listen for, etc.
   */
  hyperlang.settings.get-language

  /*
   * Making sure we prioritize "sublang" if it exists.
   */
  if
    fetch:x:/0/0?value
      file-exists:@HYPERLANG/configuration/lang-{0}.hl
        :x:/@hyperlang.settings.get-language/*/sublang?value

    /*
     * Localized file exists.
     */
    load-file:@HYPERLANG/configuration/lang-{0}.hl
      :x:/@hyperlang.settings.get-language/*/sublang?value
    return:x:/@load-file/*/*/\{0}?value
      :x:/../*/_arg?value

  else

    /*
     * Sublang file didn't exist, trying "lang".
     */
    if
      fetch:x:/0/0?value
        file-exists:@HYPERLANG/configuration/lang-{0}.hl
          :x:/@hyperlang.settings.get-language/*/lang?value

      /*
       * Localized "root" language file exists.
       */
      load-file:@HYPERLANG/configuration/lang-{0}.hl
        :x:/@hyperlang.settings.get-language/*/lang?value
      return:x:/@load-file/*/*/\{0}?value
        :x:/../*/_arg?value

    else

      /*
       * Root language file didn't exist, defaulting to "English".
       */
      load-file:@HYPERLANG/configuration/lang-en.hl
      return:x:/@load-file/*/*/\{0}?value
        :x:/../*/_arg?value
