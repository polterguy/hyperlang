
/*
 * Creates our plugin Active Event, that initialized Hyperlang, by creating
 * a widget that allows us to start Hyperlang as a plugin.
 *
 * Notice, this plugin is only created if there exists a "toolbar" widget on the 
 * current page.
 */
create-event:desktop.plugins.post.initialize-page

  /*
   * Trying to find the first widget on page with a CSS class containing the text "toolbar".
   */
  p5.web.widgets.find-first-like
    class:toolbar
  if:x:/@p5.web.widgets.find-first-like/*/*?value

    /*
     * Includes CSS file.
     */
    p5.web.include-css-file:@HYPERLANG/media/main.css

    /*
     * Making sure we insert our widget as the second last widget into our "toolbar".
     */
    p5.web.widgets.get-children:x:/@p5.web.widgets.find-first-like/*/*?value
    eval-x:x:/+/*/*
    add:x:/./*/create-widget
      src
        before:x:/@p5.web.widgets.get-children/*/0/-?value

    /*
     * Creating our button widget.
     */
    create-widget:hyperlang-start-button
      element:button
      class:hyperlang-start
      title:Start Hyperlang
      innerValue:@"<span class=""icon-flash""></span>"
      oninit

        /*
         * Checking if we have a "keep alive" session variable, across page loads,
         * and if so, make sure we initialize Hyperlang by default during pageload.
         */
        p5.web.session.get:hyperlang.keep-alive
        if:x:/@p5.web.session.get/*?value

          /*
           * Hyperlang is supposed to be load initially during pageload.
           *
           * Making sure we destroy session variable, to avoid it from loading
           * on every pageload, before we start Hyperlang.
           */
          p5.web.session.set:hyperlang.keep-alive

          /*
           * Starts Hyperlang.
           */
          hyperlang.start

      onclick

        /*
         * Starts Hyperlang.
         */
        hyperlang.start
