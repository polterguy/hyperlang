
/*
 * Creates the event that allows the user to configure a phrase to a piece of
 * Hyperlambda. User is expected to be "root" to be allowed evaluating this lambda.
 *
 * Expects [_arg] to be phrase to create association for.
 *
 * Optionally pass in [pos] being the parent phrase to create phrase to command association for.
 */
create-event:hyperlang.create-phrase2hyper-association

  /*
   * Sanity checking arguments.
   */
  micro.lambda.contract.min:x:/..
    _arg:string
  micro.lambda.contract.optional:x:/..
    pos:long
    hyperlambda:string

  /*
   * Verifying user is root, and if not, we don't allow him to evaluate event.
   *
   * The reason is that this eventallows the user to create arbitrary Hyperlambda,
   * that is evaluated on the server.
   */
  whoami
  if:x:/@whoami/*/role?value
    !=:root

    /*
     * We don't allow execution of this lambda, unless user is root.
     */
    return

  /*
   * Checking to see if we're in some specific point in our "grammar tree".
   */
  _pos
  hyperlang.position.get
  if:x:/../*/pos?value

    /*
     * Using explicitly given position from [pos].
     */
    set:x:/@_pos?value
      src:x:/../*/pos?value

  else-if:x:/@hyperlang.position.get?value

    /*
     * We're in some specific position in our tree, selecting the first phrase
     * matching our command.
     */
    set:x:/@_pos?value
      src:x:/@hyperlang.position.get?value

  /*
   * Figuring out current language.
   */
  p5.auth.my-settings.get
  split:x:/@p5.auth.my-settings.get/*/hyperlang/*/voice?value
    =:,
  split:x:/@split/0/-?name
    =:-
  if:x:/@split/*?count?value
    =:int:1

    /*
     * Deleting language radio buttons, since this is a "root language".
     */
    set:x:/../*/create-widgets/**/widgets/=radio-buttons(/.|/-/br)

  /*
   * Creating a modal widget, which asks user to type in Hyperlambda, and configure
   * a piece of Hyperlambda, which will result in associating the specified Hyperlambda
   * with the given [_arg] phrase.
   */
  eval-x:x:/+/*/*/*/h3/*/innerValue|/+/**/widgets/=radio-buttons/*(/input/*/.data-value|/span/*/innerValue)|/+/**(/_pos|/hypereval.widgets.hyperlambda-textarea)
  create-widgets
    micro.widgets.modal:hyperlang-create-phrase2hyper-association
      widgets
        h3
          innerValue:Configure <em>'{0}'</em>
            :x:/../*/_arg?value
        label
          innerValue:Synonymous phrases
          for:hyperlang-synonymous-phrases
          style:"margin-bottom:0;"
        literal:hyperlang-synonymous-phrases
          element:textarea
          class:fill
          rows:5
          placeholder:Type in synonymous phrases here, one for each line ...
          .data-field:synonyms
          value:x:/../*/_arg?value
        label
          innerValue:Hyperlambda to asociate with phrase(s)
          style:"margin-bottom:0;"
        hypereval.widgets.hyperlambda-textarea:hyper-input
          element:textarea
          .data-field:hyperlambda
          value:x:/../*/hyperlambda?value
        label
          title:Is this a global command, implying something you could tell Elizabeth at any time?
          style:"margin-bottom:0;"
          widgets
            input
              type:checkbox
              .data-field:global
            span
              innerValue:Global
        label
          title:Should this phrase only apply for you?
          style:"margin-bottom:0;"
          widgets
            input
              type:checkbox
              .data-field:private
              checked
            span
              innerValue:Private phrase
        label
          style:"margin-bottom:0;"
          widgets:radio-buttons
            input
              type:radio
              checked
              .data-field:lang
              .data-value:x:/@split/0?name
              name:hyperlang-name
            span
              innerValue:x:/@split/0?name
              title:Is this a 'main language phrase'
        label
          style:"margin-bottom:0;"
          widgets:radio-buttons
            input
              type:radio
              .data-field:lang
              .data-value:{0}-{1}
                :x:/@split/0?name
                :x:/@split/1?name
              name:hyperlang-name
            span
              innerValue::{0}-{1}
                :x:/@split/0?name
                :x:/@split/1?name
              title:Is this a 'minor language phrase'
        br
        literal
          element:label
          style:"position:absolute;bottom:0;left:2rem;"
          oninit

            /*
             * Checking if we have a position, and if so, displaying it's first
             * command, otherwise simply deleting widget entirely.
             */
            _pos:x:/../*/_pos?value
            if:x:/@_pos?value

              /*
               * Figuring out the first relevant phrase, and displaying it as
               * widget's innerValue.
               */
              .defaults
                voice:Karen,en-AU
              p5.auth.my-settings.get
              split:x:(/@p5.auth.my-settings.get/*/hyperlang/*/voice|/@.defaults/*/voice)/$?value
                =:,
              split:x:/@split/0/-?name
                =:-
              p5.mysql.connect:[hyperlang]
                p5.mysql.scalar:@"select command from phrases where commandid in (select id from commands where id = @id) and (lang = @lang or lang = @mainlang) limit 1"
                  @id:x:/@_pos?value
                  @lang:x:/@split/@split/0/-?name
                  @mainlang:x:/@split/0?name
                set-widget-property:x:/../*/_event?value
                  innerValue:@"Parent: '{0}'"
                    :x:/@p5.mysql.scalar?value

            else

              /*
               * Simply deleting widget.
               */
              delete-widget:x:/../*/_event?value

        div
          class:right
          widgets
            div
              class:strip
              style:"display:inline-block;"
              widgets
                button
                  innerValue:OK
                  style:"margin-bottom:0;"
                  onclick

                    /*
                     * Retrieving code from CodeMirror widget, and creating an
                     * association between phrase and code, before we close widget
                     * and restarts listening loop.
                     */
                    micro.form.serialize:hyperlang-create-phrase2hyper-association

                    /*
                     * Connecting to database, and inserting into "commands" and "phrases".
                     */
                    p5.mysql.connect:[hyperlang]

                      /*
                       * Forward evaluated above.
                       */
                      _pos:x:/../*/_pos?value

                      /*
                       * Checking if this is a "private" command, at which point we
                       * insert it with the currently logged in user's username.
                       */
                      if:x:/@micro.form.serialize/*/private?value

                        /*
                         * Private command, checking if it's global.
                         */
                        whoami
                        if:x:/@micro.form.serialize/*/global?value

                          /*
                           * Global command, implying no "parent" relationship.
                           */
                          p5.mysql.insert:@"insert into commands (hyperlambda, username, global) values (@hyperlambda, @username, 1)"
                            @hyperlambda:x:/@micro.form.serialize/*/hyperlambda?value
                            @username:x:/@whoami/*/username?value

                        else

                          /*
                           * Not a global command, inserting also "parent"
                           */
                          p5.mysql.insert:@"insert into commands (hyperlambda, username, parent) values (@hyperlambda, @username, @parent)"
                            @hyperlambda:x:/@micro.form.serialize/*/hyperlambda?value
                            @username:x:/@whoami/*/username?value
                            @parent:x:/@_pos?value

                      else

                        /*
                         * Public command, checking if it's global.
                         */
                        if:x:/@micro.form.serialize/*/global?value

                          /*
                           * Global command, implying no "parent" relationship.
                           */
                          p5.mysql.insert:@"insert into commands (hyperlambda, global) values (@hyperlambda, 1)"
                            @hyperlambda:x:/@micro.form.serialize/*/hyperlambda?value

                        else

                          /*
                           * Not a global command, inserting also "parent"
                           */
                          p5.mysql.insert:@"insert into commands (hyperlambda, parent) values (@hyperlambda, @parent)"
                            @hyperlambda:x:/@micro.form.serialize/*/hyperlambda?value
                            @parent:x:/@_pos?value

                      /*
                       * Splitting synonyms, and inserting one 'phrase' for each.
                       */
                      split:x:/@micro.form.serialize/*/synonyms?value
                        =:"\r\n"
                      for-each:x:/@split/*?name
                        p5.mysql.insert:@"insert into phrases (command, commandid, lang) values (@command, @commandid, @lang)"
                          @command:x:/@_dp?value
                          @commandid:x:/..p5.mysql.connect/**/p5.mysql.insert/*/id/[0,1]?value
                          @lang:x:/@micro.form.serialize/*/lang?value

                    /*
                     * Closing modal widget, and restarting listening loop.
                     */
                    delete-widget:hyperlang-create-phrase2hyper-association

                    /*
                     * Restarting input loop.
                     */
                    hyperlang.restart:[OK]

                button
                  innerValue:Cancel
                  style:"margin-bottom:0;"
                  onclick

                    /*
                     * Closing modal widget, and restarting listening loop.
                     */
                    delete-widget:hyperlang-create-phrase2hyper-association

                    /*
                     * Restarting input loop.
                     */
                    hyperlang.restart:[Talk to me]
